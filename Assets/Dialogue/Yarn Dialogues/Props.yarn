title: Props_Vars
---
<<declare $hasDoneGameDialogue = false>>
<<declare $hasDoneSocialDialogue = false>>
<<declare $hasDoneSchoolRant = false>>
<<declare $hasDoneSchoolRant2 = false>>
<<declare $didHomework = false>>
<<declare $didProjects = false>>
<<declare $didAnime = false>>
<<declare $didTwitch = false>>
<<declare $doneTrash = false>>
<<declare $isTrashDirty = false>>
<<declare $isGroupMeetingOn = false>>
<<declare $canBeatGame = false>>
<<declare $hasNotifiedCanBeatGame = false>>
<<declare $hasDoneBusDialogue = false>>
<<declare $hasDoneKabowskiToilet = false>>
<<declare $hasShitted = false>>
<<declare $isBusCutscenePlaying = false>>
<<declare $didPhone = false>>
===

title: D1_Bedroom_Window
---
<<if $day == 0>>
	Steven: The outside world... 
	<<wait 2>> 
	Steven: How scary... #e:weary
	<<jump Nothing>>
<<endif>>
<<if $gameHour > 20 || $gameHour < 6>>
	Steven: I love the nighttime... #e:smile 
	Steven: It's so soothing... because no one is outside. #e:comfy
	Steven: I would take more night walks, but I'm scared of the dark... #e:worried
	Steven: And of getting robbed... #e:weary
<<else>>
	Steven: I don't want to go outside... #e:worried 
	Steven: There's people there. #e:sad
<<endif>>
===

title: Can_Beat_Game
---
Game: You can now beat the game (if you want).
Game: To do this, use your PC.
<<set $hasNotifiedCanBeatGame = true>>
===

title: D1_Bedroom_Peripherals
---
<<if $day == 0>>
	Steven: My beloved PC. #e:comfy
	Steven: Without this, I don't know if I could live. #e:stern
	<<wait 2>>
	Steven: It's my only cope in this world... #e:think1
	Steven: My only friend. #e:think1
	Steven: And yet, it can't talk, or feel. 
	Steven: It's a source of limitless entertainment, and yet I am bored all day on it. #e:huh
	<<wait 2>>
	Steven: At the end of the day, when I turn it off.. #e:think2
	Steven: And stare my blank expression through the dark reflection of my monitor. #e:think1
	Steven: Can I really say I am happy? #e:stern
<<else>>
	Steven: Hello again, PC. #e:think1
	<<playSoundString AudioManager TurnOnPC>>
	<<wait 10>>
	<<if $day == 2>>
		Steven: Sorry for cheating on you today. #e:smile
		Steven: I was talking with real people. #e:worried
		Steven: But I promise, you're still first in my heart. #e:smile
	<<endif>>
	<<wait 2>>
	Steven: What shall we do today? #e:smile
	<<wait 1>>
	<<if $day == 2>>
		Steven: Anything goes, really. As long as its entertaining enough.. and keeps my mind off my problems. #e:stern
		Steven: Or if its just homework... well... #e:bored
		Steven: I may or may not do homework.. It depends on my mood. #e:weary
	<<endif>>

	// have we beaten the game yet (and are eligble to?)
	// if so, make this the first option
	// also this will not trigger before day 6 (to make sure all dreams are played)
	<<if $canBeatGame == true && $hasDoneGroupMeeting == false>>
		// then we arrange meetup
		-> Arrange a friend meetup! 
			<<if $gameHour < 22>>
				<<jump Arrange_Meetup>>
			<<else>>
				Steven: It's too late today. #e:stern
				Steven: I should do this tomorrow. #e:smile
			<<endif>>
		-> Do something else
	<<endif>>

	// if we have friends, ask about this first.
	// once we've talked to all friends, we unlock the final cutscene where they
	// go out IRL.
	<<if $hasMadeFriend == true>>
		Steven: I could message someone... #e:dsmile
		Steven: Who should I contact? #e:think1
		<<if $hasTextedDoomer == false && $gotDoomerNumber == true>>
			-> Dylan
				Steven: Okay, let's send him a message. #e:smile
				<<jump Message_Dylan>>
			-> Do something else
		<<endif>>
		<<if $hasTextedLaura == false && $gotLauraNumber == true>>
			-> Laura
				Steven: Let's shoot her a message. #e:smile
				<<jump Message_Laura>>
			-> Do something else
		<<endif>>
		<<if $hasTextedPepe == false && $hasPepeNumber == true>>
			-> Pepe
				Steven: Let's see if Pepe's online. #e:smile
				<<jump Message_Pepe>>
			-> Do something else
		<<endif>>
		<<if $hasTextedDoomer == true && $hasTextedLaura == true && $hasTextedPepe == true>>
			Steven: Seems like I already messaged everyone. I could message them again, or do something else.
			Steven: What to do? #e:huh
			-> Message someone again 
				Steven: Ok, who?
				-> Dylan
					Steven: Okay, let's send him a message. #e:smile
					<<jump Message_Dylan>> 
				-> Laura
					Steven: Wonder what she's up to. #e:smile
					<<jump Message_Laura>>
				-> Pepe
					Steven: Pepe's definitely online. #e:smile
					<<jump Message_Pepe>>
			-> Do something else
		<<endif>>	
	<<else>>
		Steven: I don't have anyone to message yet... #e:sad
	<<endif>>

	<<wait 2>>
	Steven: What else can I do? #e:huh
	-> Play vidya
		<<jump Vidya>>
	-> Anime
		<<jump Anime>>
	-> Read Social Media
		<<jump Social_Media>>
	-> Be productive
		<<jump Productivity>>
	-> Stare blankly at the screen
		<<wait 3>>
		Steven: ... 
		<<wait 3>>
		Steven: ... #e:bored
	<<AdvanceXMinutes TimeManager 120>>
<<endif>>
===

title: Arrange_Meetup
---
<<stopAllMusic MusicObject 1.0>>
<<wait 1>>
Steven: I've now been chatting with Pepe, Laura and Dylan pretty frequently. #e:smile 
Steven: I think I could call all 3 of them my friend now... #e:smile 
<<wait 1>>
Steven: I wonder if I could arrange a meetup for all of us... #e:dsmile 
<<wait 2>>
Steven: After all, I think we're all super lonely. #e:sad
Steven: And talking online can only go so far. 
Steven: Plus, I want them to meet each other. #e:smile
<<wait 2>>
Steven: But where should I arrange it? And what should we even do together? #e:huh
<<wait 2>>
Steven: Let's first add them to a group chat. #e:smirk
<<playSoundString AudioManager TypingSounds>>
<<wait 6>>
Pepe: WHAT IS THIS? 
<<wait 1>>
Steven: Looks like you already found this...
Pepe: @everyone @everyone @everyone @everyone #e:smile
Steven: Stop it, you bastard. #e:angry2 
Voice: Pepe is terminally online, isn't he... #e:think
Steven: Yeah.. In some ways, he's more hopeless than I am.
<<wait 2>>
Laura: ... who are you? 
Pepe: A femcel? Nah, I'm leaving... 
Steven: Wait!
Pepe: Oh, its you... I've seen your avatar somewhere before.
Laura: Good god, Steven, why are you friends with this guy!? #e:looking
Laura: He's so obnoxious... I see him online everywhere.
Pepe: Quiet, femoid. #e:rage
<<wait 2>>
Laura: See.. #e:looking
Pepe: I'm obviously joking... #e:smile
Laura: ...
<<wait 1>>
Steven: You guys know each other?
Pepe: Not really, we've never talked. #e:smile
<<wait 2>>
Dylan: What is this..
Pepe: Oh, your the dude from the gaming club.
Dylan: Yeah. #e:smile
Dylan: Pepe, was it? #e:smoking
Pepe: Yep.
<<wait 1>>
Steven: Okay, hear me out guys... #e:shy
Steven: I thought we could all meet up? #e:worried
<<wait 2>>
Pepe: LOL!
Steven: what!? #e:angry2
Pepe: It's just, we don't know each other, man.
Steven: Well, that's the point. #e:yikes
Laura: I'll come. #e:looking
<<wait 2>>
Dylan: Yeah, I'd be down for a meetup. #e:smile
Pepe: What? 
Pepe: Ok... fine. But this better not be cringe. 
Dylan: Come, Pepe. You need more friends. Steven is trying to help you. #e:smile
Pepe: Oh shut up! #e:rage
<<wait 2>>
Steven: Alright, sweet. It's on, then. #e:smile
Laura: Where are we going though? #e:looking
Pepe: Your house, of course. ( ͡° ͜ʖ ͡°) #e:smile
Laura: ...can we uninvite Pepe? #e:looking
Steven: Ok guys, what about downtown?
<<wait 3>>
Pepe: wtf, are you high?
Steven: What? We can like eat or something! #e:smile 
Laura: Steven... we are not going downtown. #e:looking
<<wait 2>>
Dylan: What about the classrooms?
Laura: Both classrooms are taken up by clubs after school.
<<wait 1>>
Steven: Wait, where do you guys live? #e:think1
Steven: Why not just my house? #e:smile
<<wait 2>>
Laura: Your... house? #e:looking-away-smile
Dylan: Steven, I'm not gay. #e:smile 
Steven: Wha- I didn't mean it like that! #e:angry2
<<wait 2>>
Pepe: They're playin with you. We'll come to your place. #e:smile
Steven: Really? #e:yikes 
Laura: Yeah. Beats the other alternatives. #e:looking-smile
Dylan: Agreed, I don't see why not. #e:smile 
<<wait 2>>
Laura: Wait, but what the hell are we doing there? #e:looking
<<wait 2>>
<<playSoundString AudioManager Crickets>>
<<wait 5>>
Pepe: Hmm, maybe I could bring food? #e:smile 
<<wait 2>>
Dylan: I could teach you guys a card game after the food. #e:smile
Laura: Okay, I can bring something too. #e:looking
<<wait 2>>
Steven: Sounds good. Maybe I'll make some food for us all. #e:smile
<<wait 2>>
Pepe: Alright gtg guys. Cya.
Dylan: See ya at Steven's house.
<<wait 2>>
Laura: Wait you idiots- when is it? #e:looking
Laura: We haven't chosen a time.
<<wait 2>>
Pepe: Hmmm...
<<wait 1>>
Dylan: What about <color=yellow>7pm tonight?</color>
<<wait 2>>
Steven: Sounds good. #e:dsmile
Laura: Okay.. I'll see you there. #e:looking-away-smile 
<<wait 5>>
Voice: Why did you do that? #e:cringe
Voice: You're gonna screw this up so bad... #e:laugh
<<wait 2>>
Steven: You're right. This could go very wrong. #e:huh
Steven: But it could go very right.. #e:smile
<<wait 1>>
Steven: Maybe I rushed into this a bit fast.. I should prepare a bit. #e:worried
<<if $gameHour < 16>>
	Steven: I got some time for now, though. I can start prepping a few hours beforehand. #e:smile
<<endif>>
<<set $isGroupMeetingOn = true>>
<<StartAllMusic MusicObject 1.0>>
<<wait 1>>
===

title: Vidya
---
Steven: I guess I could go for some gaming right now... #e:smile
<<if $hasDoneGameDialogue == false>>
	Steven: Although, games stopped being fun a long time ago... #e:huh
	<<wait 2>>
	Steven: Eventually I peaked in skill, so I started smurfing on noobs. #e:smile
	Steven: That was fun for a while, until it became boring. #e:stern
	<<wait 2>>
	Steven: So I started trolling. #e:smile
	Steven: Which was also enjoyable until it wasn't. #e:think1
	<<wait 2>>
	Steven: So now, I log on to play for what reason? #e:think2
	<<wait 3>>
	Steven: I guess... there's no reason anymore.
	Steven: ... #e:think1 
	<<set $hasDoneGameDialogue = true>>
<<endif>>
<<stopAllMusic MusicObject 0.5>>
<<wait 0.5>>
<<FadeIn LevelLoader 1.0>>
<<wait 2>>
<<playSoundString AudioManager GamingSounds>>
<<wait 29>>
<<FadeOut LevelLoader 1.0>>
<<wait 2>>
Steven: Yaaaaaaaaaay..... #e:stern
Steven: We won...... #e:stern
<<wait 1>>
Steven: So exciting.... #e:weary
<<changePlayerScoreString Manager "entertained" 1.0>>
<<changePlayerScoreString Manager "contentedness" -10.0>>
<<wait 2>>
<<StartAllMusic MusicObject 0.5>>
<<wait 0.5>>
Steven: Man, fuck games. There's nothing fun to play at all. #e:angry2
===

title: Anime
---
Steven: Okay, let's watch some anime.
<<if $didAnime == false>>
	Steven: Alright, maybe I'll try to watch anime. #e:think1
	Steven: Although, most anime these days are shit and I can't enjoy them at all.
	Steven: I feel like I exhausted all the good ones. #e:weary 
	<<set $didAnime = true>>
<<endif>>
Steven: Oh well, let's just watch it anyways... #e:bored
		<<stopAllMusic MusicObject 0.5>>
<<wait 0.5>>
<<FadeIn LevelLoader 1.0>>
<<wait 2>>
<<playSoundString AudioManager AnimeSounds>>
<<wait 35>>
<<FadeOut LevelLoader 1.0>>
<<wait 2>>
<<if $didAnime == false>>
	Steven: God, anime makes me so angry for no reason... #e:stern
	<<wait 2>>
	Steven: Angry because we don't live in such a simple world... #e:angry3
	Steven: A world where you can just try hard, and get everything you ever wanted, solely because of your efforts... #e:angry
	Steven: If only it were that easy. #e:angry
	<<wait 2>>
	Steven: All anime tries to represent perfect high school life. #e:stern
	Steven: Slice of life anime, anyways...
	Steven: That's probably why I was once addicted to it... and why many people still are. #e:weary
	<<wait 2>>
	Steven: But that's why I can't stand watching it anymore. It just reminds me of how shit my own life is. #e:bored
	<<wait 2>>
	Steven: That's probably why anime fans get shit on, too.
	Steven: It's because they over-indulge in a form of escapism... #e:think1
	Steven: Which makes them weak, and delusional over reality. #e:think2
	Steven: Otherwise known as weebs... #e:stern
<<else>>
	Steven: What a mid show... #e:bored
<<endif>>
<<changePlayerScoreString Manager "entertained" 5.0>>
<<changePlayerScoreString Manager "contentedness" -5.0>>
<<wait 3>>
<<StartAllMusic MusicObject 0.5>>
<<wait 0.5>>
===

title: Social_Media
---
Steven: Let's just scroll some random videos in my feed... #e:huh
<<if $hasDoneSocialDialogue == false>>
	Steven: I don't even know why I bother scrolling social media anymore... #e:weary
	Steven: It's not like I know any of these people personally... #e:think1
	Steven: And they sure as hell don't know me.
	<<wait 2>>
	Steven: So why do I spent so much time looking at their lives? #e:stern
	<<wait 2>>
	Steven: Probably because I have some desire to know what is considered cool, or trendy. #e:huh
	Steven: I want to know what being normal looks and feels like... 
	<<wait 2>>
	Steven: Also because it makes you feel like you're being social...
	Steven: I mean, it's in the name. <color=yellow>Social</color> media.
	<<wait 2>>
	Steven: But it's ironic, because it couldn't be further from the truth. #e:smile
	<<wait 2>>
	Steven: Social media has done nothing but isolate everyone. #e:stern
	Steven: And everyone's way too addicted to it, to admit it. #e:bored
	<<wait 2>>
	Steven: Oh well. I don't feel like using my brain right now. #e:stern
	<<set $hasDoneSocialDialogue = true>>
<<endif>>
<<stopAllMusic MusicObject 0.5>>
<<wait 0.5>>
<<FadeIn LevelLoader 1.0>>
<<wait 2>>
<<playSoundString AudioManager WatchingVideos>>
<<wait 36>>
<<FadeOut LevelLoader 1.0>>
<<wait 2>>
Steven: What a snore fest. I think I lost brain cells.
<<wait 1>>
Steven: Insert cookie cutter Tiktoker number 24129412 with his cringe Proxima Nova font... #e:bored
Steven: ...the female text to speech... #e:bored
Steven: ...and his cringy zoomer perm. #e:bored
<<wait 2>>
Steven: How does anyone find this entertaining?
<<changePlayerScoreString Manager "entertained" -10.0>>
<<changePlayerScoreString Manager "contentedness" -10.0>>
<<wait 3>>
<<StartAllMusic MusicObject 0.5>>
<<wait 0.5>>
===

title: Productivity
---
Steven: Okay, I think I should do some work.
Steven: I know I could goof off right now, but it's probably gonna just make me feel bad for procrastinating. #e:think1
Steven: How should I be productive? #e:huh
-> Homework
	Steven: You want me to do homework...?
	<<if $hasDoneSchoolRant == false>>
		Steven: You know, I haven't said it yet, but I hate school.
		Steven: Not for the traditional reasons you might think. #e:think1
		<<wait 2>>
		Steven: I've been an A student up until college. #e:stern
		<<wait 1>>
		Steven: I'm a pretty smart person, too. 
		Steven: But there's just something about the way they teach here that ticks me off. #e:angry2
		<<wait 1>>
		Steven: None of the professors give a shit about the courses they teach. #e:angry2
		Steven: And none of the content is actually interesting. #e:angry2
		<<wait 1>>
		Steven: It's just a huge scam. I pay thousands of dollars to learn a concept..
		Steven: ...that I could've googled for FREE... #e:think1
		Steven: ...and watch some random guy explain the concept better than my professor ever could. #e:think2
		Steven: Is that a joke? #e:huh
		<<wait 1>>
		Steven: It's just.. hard to take my classes seriously when that's the case. #e:weary
		Steven: Fuck school, man. #e:angry2
		Steven: The only thing keeping me here is my parents, and my sense of purpose. #e:stern
		Steven: If going to college wasn't the most normal thing to do, I would be long gone. #e:smile
		<<set $hasDoneSchoolRant = true>>
	<<endif>>
	<<wait 2>>
	Steven: Ok, let's just bullshit some random assignment that I have due tomorrow. #e:smile
	<<set $didHomework = true>>
-> Passion Projects
	Steven: Ok, I don't wanna do schoolwork. #e:stern 
	Steven: Let's learn something cool instead. #e:think1
	Steven: Something I actually give a shit about. #e:smile
	<<set $didProjects = true>>
<<stopAllMusic MusicObject 0.5>>
<<wait 0.5>>
<<FadeIn LevelLoader 1.0>>
<<wait 2>>
<<playSoundString AudioManager CodingSounds>>
<<wait 32>>
<<FadeOut LevelLoader 1.0>>
<<wait 2>>
<<if $didProjects == true>>
	Steven: Phew, that was a lot of work... #e:smile
	Steven: But it feels good to put that behind me. #e:smile
	Steven: At least I can say I was productive. #e:comfy 
	<<wait 2>>
	Steven: I'm kinda tired, too. #e:dsmile
	Steven: Using your brain a lot can definitely drain you. #e:smile
<<else>>
	Steven: That was the most braindead, mid quality work I've ever done.
	<<wait 1>>
	<<if $hasDoneSchoolRant2 == false>>
		Steven: But it feels good to speedrun an assingment like that... #e:smile
		Steven: Knowing you'll never think twice it ever again. #e:smile
		Steven: I get an odd feeling of satisfaction when I know I'm saving time... #e:comfy
		Steven: ...by spending as little of it as possible on schoolwork. #e:comfy
		<<wait 2>>
		Steven: Fuck you, Professor Smith! #e:smirk
		Steven: Give me that C, I don't give a SHIT!! #e:laugh
		<<wait 1>>
		Steven: I'm not wasting my time on your stupid ass paper! #e:laugh
		<<set $hasDoneSchoolRant2 = true>>
	<<endif>>
<<endif>>

<<set $didProjects = false>>
<<set $didHomework = false>>
<<changePlayerScoreString Manager "entertained" -10.0>>
<<changePlayerScoreString Manager "energy" -20.0>>
<<changePlayerScoreString Manager "contentedness" 30.0>>
<<StartAllMusic MusicObject 0.5>>
<<wait 0.5>>
===

title: D1_Bedroom_Trash
---
Steven: I should probably pick this up.
Steven: Should I?
-> Yes
	Steven: I should probably keep this room tidy. #e:smile
	<<GetRidOfObject CrossSceneObjects "garbage">>
	<<playSoundString AudioManager ThrowAwayTrash>>
	<<wait 5>>
	Steven: At least I can do that right... #e:smile
	<<changePlayerScoreString Manager "contentedness" 10.0>>
-> No
	Steven: Eh... It can wait. #e:think1
-> Maybe so
	Steven: Ugh... decisions. #e:weary
===

title: D1_Bedroom_Chips
---
Steven: How old are these chips? #e:think1
Steven: I can't even remember eating them. #e:weary
Game: Pick them up?
-> Yes
	Steven: I should probably clean this.
	Steven: My mom always harps me on about how I don't keep my room tidy. #e:think1
	Steven: She always said that clean rooms equated to clean minds.
	<<wait 2>>
	Steven: That's a load of bullshit. My room is clean and I'm a disaster. #e:bored
	<<wait 2>>
	Steven: But whatever. Maybe I'll just do it. #e:weary
	<<GetRidOfObject CrossSceneObjects "chips">>
	<<playSoundString AudioManager ThrowAwayTrash>>
	<<wait 5>>
	Steven: Yay. I did something with my life.. #e:comfy
	<<changePlayerScoreString Manager "contentedness" 10.0>>
-> No 
	Steven: Nah, why does it matter... #e:bored
	Steven: It's not hurting anyone by being there... #e:bored
===

title: D1_Bedroom_Lamp
---
Steven: It's a bedroom lamp...
<<if $isBedroomLampOn == true>>
	<<jump Lamp_On>>
<<endif>>
	<<jump Lamp_Off>>
===

title: D1_Commons_Door
---
Steven: ...
===

title: D1_Commons_Boxes
---
Steven: Amazon boxes. 
Steven: Dunno whose these are, but they've been sitting here forever. #e:think2
<<wait 2>>
Steven: They're definitely not mine. 
===

title: D1_Commons_Oven
---
<<if $day == 0>>
	Steven: I should really use this more... #e:think1 
	Steven: Eating cooked meals can do a lot for your mental health. #e:huh
	<<wait 2>>
	Steven: But I'm lazy. #e:bored
<<else>>
	<<if $alreadyCookedToday == true>>
		Steven: Hmm, I already cooked recently. #e:bored
		Steven: Maybe its good to wait a bit first, I'm not hungry yet. #e:weary
		<<wait 2>>
		Steven: Also, the more food I eat, the sooner I'll run out. #e:think1
		Steven: And I don't like going to the grocery store. #e:yikes
	<<else>>
		Steven: Hmmm, I guess I should cook. #e:think1
		Steven: Even though I kinda don't have the energy to. #e:weary
		Steven: If I delay it today, I'm gonna have to do it tomorrow, anyways.
		Steven: Might as well just do it now... #e:bored
		<<wait 1>>
		Game: Cook Now?
		-> Yes
			Steven: Okay, gotcha. #e:stern
			Steven: Let's hope I don't fuck it up. #e:weary
			<<playCutscene LevelLoader Cooking>>
		-> No
	<<endif>>
<<endif>>
===



title: Cooking_Food
---

<<declare $val = 0>>
<<set $val = random_range(1, 6)>>
<<playSoundString AudioManager CookingFood>>
<<wait 15>>

<<if $val <= 3>>
	Steven: Shit. #e:shy 
	Steven: I burnt it. #e:cringe
	<<playSoundString AudioManager FailSound>>
	<<changePlayerScoreString Manager "contentedness" -5.0>>
	<<changePlayerScoreString Manager "entertained" -5.0>>
	<<wait 4>>
	Steven: Oh well, still gotta eat it... #e:stern
<<else>>
	Steven: There we go, perfectly cooked! #e:comfy
	<<playSoundString AudioManager SuccessSound>>
	<<changePlayerScoreString Manager "contentedness" 5.0>>
	<<changePlayerScoreString Manager "entertained" 5.0>>
	<<wait 4>>

<<endif>>
===

title: Cooking_Food_2
---
Steven: Oh well, that was nice. #e:comfy
Steven: I'm feeling full. #e:smile
<<changePlayerScoreString Manager "energy" 50.0>>
<<changePlayerScoreString Manager "contentedness" 15.0>>
<<wait 2>>
Steven: What now? #e:think2
<<set $alreadyCookedToday = true>>
===

title: D1_Commons_Sink
---
Steven: Looks like there aren't any dirty dishes today. #e:smile
<<wait 2>> 
Steven: It'll fill up soon enough... #e:weary
===

title: Fridge
---
<<if $day == 0>>
	Steven: This is the fridge.. 
	<<wait 2>>
	Steven: If you've ever lived with others you know...
	Steven: Your food isn't safe in here. #e:yikes
	Steven: Kabowski eats my shit all the time. #e:laugh
	<<wait 2>>
	Steven: But as revenge, I eat his stuff... #e:dsmile
	Steven: So I guess we're even!? #e:smile
<<endif>>
<<playCutscene LevelLoader UseFridge>>

===

title: Fridge_Dialogue
---
<<if $hasEatenSnack == true>>
	Steven: Hmm, I probably should wait a bit before eating another snack. #e:stern
	Steven: I don't wanna overindulge and run out later in the week... #e:yikes
<<else>>
	Steven: Let's see what's in here...
	-> Hot Pocket
		Steven: Hot pockets. #e:comfy
		Steven: More than 60 percent of your saturated fat, and 29 percent of your daily sodium allowance! #e:comfy 
		<<FadeIn LevelLoader 1.0>>
		<<wait 2>>
		<<playSoundString AudioManager Microwave>>
		<<wait 8>>
		<<playSoundString AudioManager CrunchyBite>>
		<<wait 3>>
		<<FadeOut LevelLoader 1.0>>
		<<wait 2>>
		<<changePlayerScoreString Manager "energy" 10.0>>
		<<changePlayerScoreString Manager "entertained" 5.0>>
		<<wait 2>>
	-> Ice Cream
		Steven: I can get behind some ice cream. #e:smile
		<<FadeIn LevelLoader 1.0>>
		<<wait 2>>
		<<playSoundString AudioManager LickIceCream>>
		<<wait 2>>
		<<FadeOut LevelLoader 1.0>>
		<<wait 2>>
		<<changePlayerScoreString Manager "energy" 5.0>>
		<<changePlayerScoreString Manager "entertained" 10.0>>
		<<wait 2>>
	-> Beer
		Steven: Hmmm. Beer would be nice. #e:smile
		Steven: But I'm definitely gonna feel a little drowsy from it. #e:think1
		Steven: I don't have the best alcohol tolerance. #e:smile
		<<FadeIn LevelLoader 1.0>>
		<<wait 2>>
		<<playSoundString AudioManager BeerOpen>>
		<<wait 8>>
		<<FadeOut LevelLoader 1.0>>
		<<wait 2>>
		Steven: Woah.. this one is strong... #e:weary
		<<changePlayerScoreString Manager "energy" -10.0>>
		<<changePlayerScoreString Manager "entertained" 20.0>>
		<<wait 2>>
	-> Carrots
		Steven: God, I hate carrots. #e:cringe
		Steven: But I can't deny that eating vegetables makes me feel a bit better. #e:smile
		Steven: Why do all the bad tasting things have to be good for you? #e:weary
		<<wait 2>>
		<<FadeIn LevelLoader 1.0>>
		<<wait 2>>
		<<playSoundString AudioManager EatingCarrots>>
		<<wait 6>>
		<<FadeOut LevelLoader 1.0>>
		<<wait 2>>
		Steven: Tastes like shit! #e:cringe
		<<wait 1>>
		Steven: Oh well, at least that was healthy, right? #e:dsmile
		<<changePlayerScoreString Manager "energy" 5.0>>
		<<changePlayerScoreString Manager "entertained" -5.0>>
		<<changePlayerScoreString Manager "contentedness" 10.0>>
		<<wait 2>>
	<<set $hasEatenSnack = true>>
<<endif>>
===

title: D1_Commons_Trash
---
<<if $isTrashDirty == false>>
	Steven: The trash is taken out. #e:think1
	Steven: Wonder who did it. #e:smile
<<elseif $isTrashDirty == true>>
	Steven: Guess I'll clean this up... #e:think1
	<<FadeIn LevelLoader 1.0>>
	<<wait 1>>
	<<playSoundString AudioManager TrashDumpster>>
	<<wait 6>>
	<<SetClean trashcan>>
	<<FadeOut LevelLoader 1.0>>
	<<wait 1>>
	<<if $doneTrash == false>>
		Steven: Why do I even bother cleaning the trash for everyone? #e:think2 
		Steven: I don't want to be their garbage mule... #e:angry2 
		<<wait 1>>
		Steven: Oh well, At least it makes the common area look a little nicer... #e:dsmile
		<<set $doneTrash = true>>
	<<endif>>
	<<changePlayerScoreString Manager "energy" -2.0>>
	<<changePlayerScoreString Manager "contentedness" 3.0>>

<<endif>>
===

title: D1_Commons_Sofa
---
Steven: I can't remember the last time I sat on this sofa. #e:think2
Steven: I probably still can't get that image out of my head of that one time Kabowski barfed on it. #e:cringe
Steven: Kabowski was trying to impress girls and he probably drank way too much... #e:yikes
===

title: D1_Commons_DiningTable
---
Steven: It's just a normal dining table. 
Steven: Cleaner than usual today.. #e:smile 
===

title: Running_Shoes
---
<<if $day == 0>>
	Steven: My running shoes... 
	Steven: Maybe I'll go run tomorrow. I'm too tired today. #e:huh
<<else>>
	<<if $hasRanToday == true>>
		Steven: I already ran today... #e:think1
		Steven: Maybe tomorrow. #e:dsmile 
		<<jump Nothing>>
	<<endif>>
	Steven: Should I go for a run? #e:stern
	-> Yeah
		Steven: Okay..
		Steven: Maybe it'll help cheer me up. #e:dsmile
		<<stopAllMusic MusicObject 1.0>>
		<<FadeIn LevelLoader 1.0>>
		<<wait 2>>
		<<playSoundString AudioManager Running>>
		<<wait 10>>
		<<FadeOut LevelLoader 1.0>>
		<<wait 2>>
		Steven: Huh, that actually felt kinda nice. #e:smile
		<<AdvanceXMinutes TimeManager 60>>
    	<<changePlayerScoreString Manager "contentedness" 30.0>>
		<<changePlayerScoreString Manager "energy" -20.0>>
		<<StartAllMusic MusicObject 1.0>>
		<<wait 2>>
		<<set $hasRanToday = true>>
	-> Nah
		Steven: Yea.. never mind. #e:yikes
<<endif>>
===

title: D1_Commons_TV
---
Steven: There's nothing good on television these days. #e:think2
Steven: Well, at least in my opinion. #e:think1
<<wait 2>>
Steven: I guess plenty of people still enjoy it, particularly boomers. #e:yikes
===

title: D1_Commons_Microwave
---
<<if $day == 0>>
	Steven: The microwave oven.
	Steven: What a marvelous invention. #e:smile
	<<wait 2>>
	Steven: I'll eat tomorrow. I'm not hungry right now.
<<else>>
	<<if $hasMicrowavedToday == false>>
		Steven: Hmm, should I microwave something real fast?
		-> Yes
			Steven: Okay, what?
			-> Popcorn
				Steven: Alright, let's have a snack.. #e:smile
			-> Canned Soup
				Steven: Canned soup? Sure. #e:smile
			-> Hot Pocket
				Steven: Not the healthiest, but it has calories. So I'm not gonna complain. #e:smile
			<<playSoundString AudioManager MicrowaveAndEat>>
			<<wait 20>>
			<<set $hasMicrowavedToday = true>>
			Steven: Yummy... #e:comfy
		-> No
			Steven: Yeah, never mind...
	<<else>>
		Steven: I already ate a microwaved meal today...
		Steven: Let's wait a bit. 
	<<endif>>
<<endif>>


===

title: D1_Brain_Painting
---
Steven: Brain is quite the space fanboy, huh.
Steven: I wish I was as passionate about something as Brain is for science. #e:weary

===
title: D1_Brain_FumeHood
---
Steven: A professional grade fume hood? #e:shocked
Steven: This wasn't here last year... How did he get this? #e:worried
<<wait 2>>
Steven: And what does he do with this... Wait- #e:embarassed
Steven: Does this mean he does experiments in the house? #e:yikes
<<wait 2>>
Steven: That bastard. #e:stern
Steven: He'll probably set us all on fire...
Steven: Or poison us all with carbon monoxide.
<<wait 2>>
Steven: Actually who am I kidding, I really don't give if that happens. #e:bored
Steven: It's not like I'm excited to be alive. #e:weary
===
title: D1_Brain_Mainframe
---
Steven: Wow, look at these mainframe computers. #e:shocked
<<wait 1>>
Steven: They're like, straight out of IBM or something. #e:think1
Steven: Wonder what he needs all that processing power for. #e:think2
<<wait 1>>
===
title: D1_Brain_Desk
---
Steven: This desk is like twice the size of mine... #e:weary
<<wait 2>>
Steven: Looks like he's running some sort of simulation. #e:think1
===
title: D1_Brain_Hangup
---
Steven: Why are there just a bunch of chemicals on a tall shelf? #e:worried
<<wait 1>>
Steven: As smart as Brain is, it looks like no one taught him safety. #e:huh
Steven: Or maybe, he just doesn't care. #e:think1
<<wait 2>>
Steven: There's fumes coming out of the yellow one... #e:bored
Steven: Maybe I should tell him when he gets back.
===
title: D1_Brain_Bed
---
Steven: Brain's bed is much smaller than mine. 
Steven: To be fair, he's a small guy.
Steven: He has atoms on his blanket. I wonder if he dreams about science. #e:smile
===

title: D1_Kab_Poster
---
Steven: This poster summarizes Kabowski's life surprisingly well.
<<wait 1>>
Steven: Minus the sex part. #e:bored
Steven: The guy would probably sell his liver for one more inch on his biceps. #e:bored
===
title: D1_Kab_Bench
---
Steven: Jeez, this bar looks heavy. #e:worried
Steven: Maybe I could lift it- should I try?
-> No, you're too weak
	Voice: No, you weakling! You'll embarass yourself!! 
	<<wait 2>>
	Steven: Yeah, you're right. I've heard people die from these things... #e:worried 
	<<wait 1>>
	Steven: I would probably need a spotter first. #e:worried 
-> Yessir, suns out guns out
	Steven: Alright, if you say so.
	<<FadeIn LevelLoader 0.5>>
	<<wait 1>>
	<<playSoundString AudioManager RobotWorkout>>
	<<wait 32>>
	<<FadeOut LevelLoader 0.5>>
	<<wait 1>>
	Steven: That was kinda tiring actually... #e:weary
	Steven: But it felt nice... #e:smile
	<<changePlayerScoreString Manager "energy" -20.0>>
	<<changePlayerScoreString Manager "contentedness" 20.0>>
	<<wait 3>>
-> Maybe..
	Steven: Oh well... maybe later... #e:think1
===
title: D1_Kab_Bed
---
Steven: Kabowski's bed is pretty big.
Steven: According to him, it's "big enough for all the girls" he brings over. #e:smile
<<wait 2>>
Steven: And of course it isn't made. What a surprise. #e:think1
===
title: D1_Kab_Kotatsu
---
Steven: Hmm, this seems like one of those japanese heated table things.
Steven: What's the name again? 
-> Kotatsu 
	Steven: Yes, that's it. I always wondered how sitting under one feels... #e:think1
-> IDK
	Steven: Hmm, beats me too. #e:think1
Steven: Kabowski seems like one of those dudes who just buys things on a spur.
Steven: Maybe I should do that too. It might make my life more interesting. #e:smile
===

title: D1_Bathroom_SinkAndMirror
---
Steven: Looking at the mirror always makes me sad.
Steven: My appearance has changed so much, and yet I've grown so little on the inside. #e:bored
<<wait 2>>
Game: Brush teeth?
-> Yes
	<<if $teethBrushed == true>>
		Steven: I already brushed today... #e:think2
		<<jump Nothing>>
	<<endif>>
	<<FadeIn LevelLoader 1.0>>
	<<wait 2>>
	<<playSoundString AudioManager BrushTeeth>>
	<<wait 10>>
	<<changePlayerScoreString Manager "contentedness" 10.0>>
	<<FadeOut LevelLoader 1.0>>
	<<wait 2>>
	<<set $teethBrushed = true>>
	Steven: I might be a loser, but at least I got pearly whites... #e:comfy
-> No
	Steven: ... #e:think1
===

title: D1_Bathroom_Shower
---

Game: Shower?
-> Yes
	Steven: Wait a second... #e:angry2
	<<wait 2>>
	Steven: Are you trying to kill me? #e:angry3
	<<wait 2>>
	Steven: I'm a robot, I can't shower... #e:angry3
-> No
	Steven: Why would I shower if I don't exercise or sweat?
	<<wait 2>>
===
title: D1_Bathroom_Toilet
---
<<if $hasDoneKabowskiToilet == false>>
	Steven: Seems like Kabowski was kind enough to aim this time... #e:smile 
	<<wait 2>>
	Steven: Good for him. #e:smile 
	Steven: I was worried he wasn't potty trained. #e:think1
	<<set $hasDoneKabowskiToilet = true>>
<<endif>>
Game: Take a fat shit?
-> Hell Yeah
	<<if $hasShitted == true>>
		Steven: Hmm, I would if I could...
		Steven: But I have nothing in the tank right now... #e:sad
		<<jump Nothing>>
	<<endif>>
	Steven: Taking a fat shit is arguably the best part of my day. #e:dsmile 
	Steven: Now get out of here and let me enjoy my shit. #e:comfy
	<<FadeIn LevelLoader 1.0>>
	<<wait 2>>
	<<playSoundString AudioManager TakeShit>>
	<<wait 25>>
	<<changePlayerScoreString Manager "contentedness" 10.0>>
	<<FadeOut LevelLoader 1.0>>
	<<wait 2>>
	Steven: Oh... #e:smile
	Steven: that hit the spot... #e:comfy
	<<set $hasShitted = true>>
-> No
	Steven: ... #e:sad
===

title: D1_Lancelot_PolarBear
---
Steven: I'm not sure how Nikolai was able to afford this. #e:shocked
Steven: It feels like real polar bear skin. #e:think2
<<wait 2>>
Steven: I think this is his bed, since I don't see anything else around. #e:think1
===
title: D1_Lancelot_Desk
---
Steven: A gun, some weed, and cigarettes. #e:think1
<<wait 2>>
Steven: Nikolai is not as wholesome a guy as I thought... #e:worried
===
title: D1_Lancelot_Discoball
---
Steven: This is one of those professional grade discoballs.
===
title: D1_Lancelot_Subwoofers
---
Steven: I've been tempted many times last semester to throw a bucket of water at these when Nikolai is gone. #e:stern
Steven: So tempting... #e:angry3
===
title: D1_Lancelot_NorthKorea
---
Steven: Nikolai isn't even Korean. He's Russian. 
Steven: I dunno why he has this. #e:think1
<<wait 2>>
Steven: Communism, I guess... #e:huh
===

title: D2_House
---
Steven: My house looks quite nice on the outside. #e:smile
Steven: But then you realize that it's leased to four other tenants.
Steven: So on top of my roomates, I get to deal with three other groups of people. #e:stern
<<wait 1>>
Steven: Last year, we had a group of rowdy seniors who lived next door.
<<wait 2>>
Steven: They kept having really loud sex. #e:angry3
<<wait 1>>
Steven: Probably with one another. #e:angry
===

title: D2_Neighbors_House
---
Steven: This entire neighborhood is jam-packed with these leasable houses. #e:think1
<<wait 1>>
Steven: They're owned by some predatory leasing company who could care less about their tenants.
<<wait 1>>
Steven: I don't ever talk to my neighbors, but I see them coming in and out of the house.
<<wait 2>>
Steven: I think a few girls live here? #e:huh
Steven: I'd rather not get caught snooping around their house... #e:yikes
<<set $hasEnteredOutsideHouseBefore = true>>
===

title: D2_Bus_Stop_House
---
<<if $hasDoneBusDialogue == false>>
	Steven: I have to take the bus to get to my classes.
	Steven: I live about a few miles away from campus, after all. #e:sad
	<<wait 1>>
	Steven: Every time you take the bus, you play this one game called.. #e:think2
	Steven: "Will it arrive on time??" #e:smirk
	<<set $hasDoneBusDialogue = true>>
<<endif>>

<<if $day == 2>>
	Steven: Hmm... it says "Bus routes start tomorrow..." #e:shocked
	<<wait 1>>
	Steven: Guess the drivers still haven't come back from their break. #e:smirk
	<<wait 1>>
	Steven: I don't blame them, being a bus driver must be hell. #e:think1
	<<wait 2>>
	Steven: Welp, looks like I'm walking today. #e:bored 
<<else>>
	<<declare $minTilNextBus = 10>>
	<<set $minTilNextBus = random_range(5, 20)>>
	Game: Would you like to ride the bus? The next one is {$minTilNextBus} minutes away

	-> Yes
		Steven: Alright.
		<<set $isBusCutscenePlaying = true>>
		<<playCutsceneAndFade LevelLoader TakeBusToCampus Campus>>
	-> No
		Steven: ... 
<<endif>>

===

title: Play_Phone_Start
---
Steven: Let's turn off my brain and watch some stupid shit... #e:dsmile
<<stopAllMusic MusicObject 1.0>>
<<wait 2>>
===

title: D2_Bus_Stop_Campus
---
<<if $day == 2>>
	Steven: Hmm... it says "Bus routes start tomorrow..." #e:think1
<<else>>
	Game: Would you like to ride the bus? The next one is {$minTilNextBus} minutes away
	-> Yes
		Steven: Alright.
		<<set $isBusCutscenePlaying = true>>
		<<playCutsceneAndFade LevelLoader TakeBusToHouse OutsideHouse>>
	-> No
		Steven: ... 
<<endif>>
===

title: After_Waste_Phone
---
Steven: Wow this is giving me cancer... #e:cringe
<<changePlayerScoreString Manager "entertained" 10.0>>
<<changePlayerScoreString Manager "contentedness" -10.0>>
<<wait 2>>
Steven: Yet it's kinda addicting. #e:dsmile
Steven: What am I doing with my life... #e:bored 
<<set $didPhone = true>>
===

title: Set_Bus_Playing_False
---
Steven: ... #e:bored
<<set $isBusCutscenePlaying = false>>
===

title: D2_Campus_Apartment
---
<<playCutscene LevelLoader LookAtApartmentBuilding>>
===

title: D2_Campus_Apartment_Text
---
Steven: Some of these high-rise apartments are actually quite nice, since they're so close to school.
Steven: The only problem with them is the residents...
Steven: They're all rich, spoiled, and extremely loud... #e:stern
===

title: Restaurant
---
Steven: It's a building with lots of restaurants in it. #e:smile
<<wait 1>>
Steven: I usually don't eat out.. #e:stern
Steven: Not that I don't have the money to... #e:think2
Steven: But because I'm scared of talking to the cashier.. #e:sad
===

title: Hair_Salon
---
Steven: I don't have hair, but this seems to be a pretty decent spot for a haircut. #e:smile
===

title: Bar
---
Steven: It's the local bar. #e:smile
<<wait 2>>
Steven: I heard they throw parties at 9PM every night? #e:smile 
Steven: At least, that's what Kabowski told me. #e:think1
===

title: Ice_Cream
---
Steven: Hmm, wouldn't be a college campus without some kind of Boba place, right? #e:bored
Steven: They have a nice rooftop lounge as well.. not that I've been there. #e:sad
<<wait 1>>
Steven: It's fine though, I don't like boba too much... too sugary. #e:dsmile
===

title: Lecture_Hall_Sit_Down
---
Game: Sit down here?
-> Yes
	<<if GetGameVariable("HasFinishedLecture") == false>>
		<<SetGameVariable "HasFinishedLecture" true>>
		<<playCutscene LevelLoader LectureCutscene>>
	<<endif>>
-> No
===

title: Lecture_Hall_Sit_Down_Occupied
---
Game: You try to sit down on someone...
Stranger: Hey! I'm sitting here!
===

title: Coffee_Girl
---
Steven: No way I'm gonna get something from there... #e:worried
Steven: Talking to the cashier is never worth the stress. #e:bored 
<<wait 2>>
Steven: I'll probably embarass myself and stutter a ton... #e:bored
<<wait 2>>
Steven: The only way I could do it is if I was with a friend.. #e:sad
Girl: Hi! Can I get you something?
<<wait 2>>
Steven: .. #e:oshit
Steven: No thanks! #e:embarassed
===