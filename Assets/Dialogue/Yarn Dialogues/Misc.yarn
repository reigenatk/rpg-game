title: Declarations
---
// one time events
<<declare $hasBrainSmashedGlassYet = false>>
<<declare $acceptedBibleStudy = false>> // by default we haven't met stacy yet
<<declare $hasGoneToBibleStudy = false>>
<<declare $saidStacyWasHotInFrontOfLaura = false>>
<<declare $coomerWasExposed = false>>
<<declare $hasHeardReaperTerms = false>>

// reset these every day
<<declare $teethBrushed = false>>
<<declare $isBedroomLampOn = false>>
<<declare $numTimesNutted = 0>>
<<declare $wasNiceToRoomates = false>>
<<declare $talkedToNikolaiAlready = false>>
<<declare $talkedToKabowskiAlready = false>>
<<declare $talkedToBrainAlready = false>>
<<declare $talkedToStacyAlready = false>>
<<declare $talkedToBeckyAlready = false>>
<<declare $talkedToDoomerAlready = false>>
<<declare $talkedToDoomerGirlAlready = false>>
<<declare $talkedToPepeAlready = false>>
<<declare $talkedToBoomerAlready = false>>
<<declare $talkedToBloomerAlready = false>>
<<declare $talkedToCoomerAlready = false>>
<<declare $talkedToRedditAlready = false>>
<<declare $talkedToDiscordAlready = false>>
<<declare $alreadyCookedToday = false>>
<<declare $hasEatenSnack = false>>
<<declare $hasTalkedToReaper = false>> 

// local variables
<<declare $contentednessNerf = 0>>
<<declare $entertainedBuff = 0>>

// variables set by c# code and events (time + location based)
<<declare $minTilNextBus = 1>>
<<declare $day = 1>>
<<declare $isInNikolaiRoom = false>>

// time events
<<declare $isBibleStudyOpen = false>>
<<declare $isStalkingStacy = false>>
<<declare $isFootballGame = false>>
<<declare $isGamingClub = false>>
<<declare $hasGoneToGamingClub = false>>
// total counts
<<declare $numTimesNiceToRoomates = 0>>
<<declare $kabowskiFriendProgress = 0>>
<<declare $nikolaiFriendProgress = 0>>
<<declare $brainFriendProgress = 0>>
<<declare $stacyFriendProgress = 0>>
<<declare $beckyFriendProgress = 0>>
<<declare $doomerFriendProgress = 0>>
<<declare $doomerGirlFriendProgress = 1>>
<<declare $pepeFriendProgress = 0>>
<<declare $boomerFriendProgress = 1>>
<<declare $bloomerFriendProgress = 0>>
<<declare $coomerFriendProgress = 0>>
<<declare $numTimesCoomerExposed = 0>>
<<declare $RedditFriendProgress = 0>>
<<declare $DiscordFriendProgress = 0>>


===

title: LowEnergy
---
Jeez, I'm tired...
I should probably sleep soon.
===

title: OutOfEnergy
---
M-must...
stay...
awake-
<<playCutscene LevelLoader EnergyZero>>
===

title: DoorIsntOpen
---
<<playSoundString AudioManager LockedDoor>>
<<wait 3>>
The door isn't open.
Knock?
-> Yes
    <<KnockOnDoor Manager>>
-> No
===


title: GoToSleep
---
It's pretty late...
Should I sleep?
-> Yes
	Alright...
	<<if $teethBrushed == false>> 
		Would you like to brush your teeth first?
		-> Ok, fine.
			<<stop>>
		-> No, I'm too depressed to brush my teeth.
	<<endif>>
	<<goToSleep LevelLoader>>
-> Hell No!
===

title: CantGoToSleep
---
<<if $day == 1>>
	Ah yes, the bed.
	Where I spend 90% of my waking time.
	Staring at the ceiling, waiting for the day to end.
	<<wait 1>>
	And that's 8 hours where I don't need to be awake. 
<<elseif $day == 2>>
	I can't sleep yet.
	I know there's nothing to do, but lying in bed will only make me feel worse.
	Let's try to find something to do, my problems won't fix themselves.
<<endif>>
===

title: EndOfDayDialogue
---

<<if $day == 1>>
    Well, what an utterly unproductive day.
    I have my first lectures tomorrow...
    Even as a 20-year old, I still dread the first day of school.
    Icebreakers, syllabuses, and annoying people.
    Let's just get through it.
<<elseif $day == 2>>
	What a long day.
	It sounds sad to say, but this was probably my most eventful day so far this year.
	<<wait 1>>
	Which is funny, because I didn't even do that much.
	<<wait 1>>
	Walking to lecture, talking to roommates, hell, even a girl.
	Most people would shrug that off as a normal day in the life.
	<<wait 1>>
	But it's special, even hard for me to do.
	<<wait 1>>
	At least I can go to sleep today, thinking that I tried.

<<else>>

<<endif>>
===

title: Lamp_On
---
Would you like to turn it off?
-> Yes
	<<set $isBedroomLampOn to false>>
	<<toggleLight Lamp false>>
-> No
===

title: Lamp_Off
---
Would you like to turn it on?
-> Yes
	<<set $isBedroomLampOn to true>>
	<<toggleLight Lamp true>>
-> No
	Moonlight drowns out all but the brightest stars.
===

title: Knock_No_Response
---
There was no response...
===

title: COOM_DEMON
---
Would you like to summon...
The coom demon?
<<if $numTimesNutted > 0>>
	Be warned that summoning the coom demon multiple times a day is not recommended.
	Side effects include lethargy, a dose of shame, and diminishing returns.
<<endif>>

-> Yes
	If you say so...
	<<playCutscene LevelLoader Coom>>
-> No
	On a nofap streak, eh?
===

title: PostNutClarity
---
<<set $contentednessNerf = round(random_range(1, 4) * 10)>>
<<if $numTimesNutted > 0>>
	<<set $contentednessNerf = $contentednessNerf + 5*$numTimesNutted>>
<<endif>>
<<if $contentednessNerf >= 30>>
	Game: The post nut clarity hits you hard...
	What the hell am I doing with my life? 
	Game: You lose {$contentednessNerf} contentendess!
	<<changePlayerScoreString Manager "contentedness" {-1 * $contentednessNerf}>>
<<else>>
	Game: The post-nut clarity hits you, but you're used to it now.
	A necessary sacrifice.
	Game: You lose {$contentednessNerf} contentendess!
	<<changePlayerScoreString Manager "contentedness" {-1 * $contentednessNerf}>>
<<endif>>

<<set $entertainedBuff = round(random_range(0, 6) * 5)>>
<<if $entertainedBuff >= 15>>
	That did feel pretty good, though.
	Game: You gain {$entertainedBuff} entertainment!
	<<changePlayerScoreString Manager "entertained" {$entertainedBuff}>>
<<else>>
	Game: Your dopamine receptors don't get much pleasure from this anymore..
	Jerking off doesn't even make me feel that much better anymore...
	Game: You gain {$entertainedBuff} entertainment.
	<<changePlayerScoreString Manager "entertained" {$entertainedBuff}>>
<<endif>>
<<set $numTimesNutted = $numTimesNutted + 1>>
Was it worth it?
===

title: WalkToCampus
---
Should I walk to campus? (-5% energy, +20 minutes, +5% contentedness)
-> Yes
    <<changePlayerScoreString Manager "contentedness" 5.0>>
    <<changePlayerScoreString Manager "energy" -5.0>>
    <<AdvanceXMinutes Manager 20>> 
    <<yarnLoadScene LevelLoader Campus 2.0>>
-> No, I'll ride the bus
===

title: WalkToHome
---
Walk home? (-5% energy, +20 minutes, +5% contentedness)
-> Yes
    <<changePlayerScoreString Manager "contentedness" 5.0>>
    <<changePlayerScoreString Manager "energy" -5.0>>
    <<AdvanceXMinutes Manager 20>> 
    <<yarnLoadScene LevelLoader OutsideHouse 2.0>>
-> No, I'll ride the bus
===


title: AlreadyTalkedToday
---
Mind: I already talked to them today... 
Mind: It might be awkward if I talk to them again so soon. #e:stern
Mind: Let's wait until tomorrow. #e:shy
===

title: BibleStudy
---

Chase: Everyone! Let's get started. 
<<wait 1>>
Chase: Actually, before we begin, let's go around the room and introduce ourselves.
<<wait 1>>
Chase: Steven, how about you go first?
===

title: BibleStudy_Introductions
---
Introduce yourself?
-> Yes
	Steven: Uhh, h-hi guys, I'm Steven. #e:shy
	<<wait 2>>
	Voice: Tell them more, dumbass...
	<<wait 2>>
	Steven: My favorite color is Red. 
	<<wait 2>>
	Voice: No! Not that! Haha you sound so stupid. #e:cringe
	Steven: Uhhh... I'm a junior...
	<<wait 4>>
	Steven: Uhm...
	<<wait 2>>
	Mind: Shit! I'm freezing up... This is bad... #e:oshit
	<<changePlayerScoreString Manager "social" -40.0>>
	<<wait 2>>
	Chase: Thanks for introducing yourself, Steven! I love the color red too! #e:big-smile
	<<changePlayerScoreString Manager "social" 30.0>>
	<<wait 2>>
	Mind: At least Chase is nice enough to cover up for me. #e:weary
	Voice: You did so bad! #e:laugh
-> Hell No
	<<wait 6>>
	Chase: It's okay if you don't want to. Guys, this is Steven. 
	<<wait 3>>
	Voice: You awkward fuck, you shoulda just introduced yourself. 
	Voice: Look at them, judging you. #e:laugh
	<<changePlayerScoreString Manager "social" -20.0>>
	<<wait 3>>
<<jump Other_Introductions>>
===

title: Other_Introductions
---
Chase: Let's go around the room.
<<wait 2>>
Keith: Hi, I'm Keith. I'm a sophomore.
<<wait 2>>
Pepe: Hello, I'm Pepe. I'm studying mechanical engineering.
<<wait 2>>
Laura: Hey, I'm Laura.
<<wait 2>>

Voice: Awkward... #e:cringe
-> Say something back to everyone
	Steven: Nice to meet you guys...
-> Say nothing
	<<wait 4>>
	<<changePlayerScoreString Manager "social" -10.0>>
	Voice: You probably shoulda said something... #e:cringe
===

title: Stacy_Arrive
---
Stacy: I'm here! 
<<wait 1>>
Chase: Hey Stacy! Perfect timing, we were just gonna start.
Stacy: yay! How's everyone doing?
<<wait 2>>
Pepe: Good, I did the prayer thing you told me to do last time.
<<wait 2>>
Stacy: Awesome! Did you feel God's presence with you at all?
<<wait 1>>
Pepe: Yeah, a bit. I guess it helped me feel a bit better. #e:smile
Pepe: But it still feels like I'm talking to no one at times. #e:sad
<<wait 2>>
Chase: God speaks to each of us in his own way, Pepe.
Chase: I'm glad that you are trying!
<<wait 1>>
Stacy: How about you, Keith? How was your day?
Keith: Pretty rough. I tried my best to avoid my, uh...
Keith: My... problems #e:sad
<<wait 1>>
Chase: Thanks for sharing, Keith. We all deal with temptation in various ways.
Chase: But remember, God has blessed us with the Holy Spirit, which is stronger than all evil.
Stacy: We will pray for you, Keith! #e:smile
Keith: T-thanks, stacy!
<<wait 4>>
Laura: I'm assuming I need to share? #e:looking
Stacy: Only if you want, Laura! #e:smile
Laura: ...
Laura: I guess I tried reading the Bible a bit.
Laura: Not that any of it really makes sense to me... #e:looking
<<wait 1>>
Laura: I still don't really get what Christianity is about.
Laura: Do you guys really believe in a heaven and hell? #e:neutral
Laura: That somehow, magically, we will live past death? It sounds like a fairytale to me.
<<wait 2>>
Stacy: It is not the work of magic, but faith, that keeps us steady, my dear friend.
Stacy: The world would be bleak if not for the sacrafice of Jesus. #e:smile
<<wait 1>>
Chase: Amen. #e:slight-smile
Chase: We'll learn more about this in the coming weeks. I'm glad you started to read ahead! #e:big-smile
<<wait 3>>
Stacy: Steven! I'm going to pick on you! Tell us why you decided to come today. #e:smile
<<wait 1>>
Voice: Shit! Don't mess this up! #e:laugh
-> I want to make new friends
	Steven: I, er...
	<<wait 1>>
	Steven: I want to make new friends... #e:embarassed
	<<wait 2>>
	Pepe: That's why I came originally, too. #e:smile
	Pepe: Everyone here is really nice, you'll like it.
	<<wait 1>>
	Chase: Just like he said, we're your friends now!
	Chase: Feel free to ask us for anything, even outside of here.
	<<wait 1>>
	-> Thanks, guys
	-> ...
-> I think you're really hot
	Steven: Because you look absolutely stunning tonight.
	<<wait 2>>
	<<playSoundString AudioManager crickets>>
	<<wait 5>>
	<<changePlayerScoreString Manager "social" -10.0>>
	Voice: Hahaha what are you doing!?
	Stacy: Uhm... thanks, Steven. #e:laughing
	<<wait 1>>
	<<set $saidStacyWasHotInFrontOfLaura = true>>
-> I want to learn more about God
	Steven: I guess I want to learn more about religion...
	Steven: I've never gotten into the whole "church" thing.
	<<wait 1>>
	Chase: Well that's why we're here! #e:slight-smile
	Chase: It's suprising since in today's world, not many people care or know about God.
	Chase: No one wants to be bossed around, or follow rules.
	<<wait 1>>
	Stacy: But being a Christian is much more than that! It's not just a rulebook.
	Stacy: It's a calling, a purpose in an otherwise bleak life. #e:smile
	<<wait 2>>
	Stacy: My roommate, Laura, is also new here. She's trying to grapple with it as well, so don't worry! 
	<<wait 1>>
	Laura: ... 
	Laura: I only came here because you told me to... #e:looking
	Laura: But I guess it's been a nice change of pace from my routine life #e:looking-away-smile
	Stacy: Exactly! A little force was required to get her to show up, but she enjoys it #e:laughing

<<if $saidStacyWasHotInFrontOfLaura == true>>
	Chase: Well, err, let's get rolling, shall we! #e:confused
<<else>>
	Chase: Well, I'm glad we could all meet one another. Let's get going!
<<endif>>
===

title: After_Sermon
---
	Stacy: Thank you for coming, everyone!
	Chase: Can we just come together for a quick prayer?
	<<FadeIn LevelLoader 1.0>>
	<<playSoundString AudioManager Prayer>>
	<<wait 8.5>>
	<<FadeOut LevelLoader 1.0>>
	Chase: Have a great week, guys!
	<<wait 1>>
	Mind: That was fun. It feels good to be around people, even though I may not be very religious.
	<<wait 2>>
	Voice: You still did a terrible job #e:laugh
	<<wait 1>>
	Mind: Whatever. I didn't expect to do great. At least some people know who I am now.
	<<changePlayerScoreString Manager "social" 50.0>>
	<<set $hasGoneToBibleStudy = true>>
	<<SetTime Manager 20 50 0>>
===

title: Talk_To_Reaper
---
// test the cutscene (skip the dialogue)
// <<playCutsceneAndFade LevelLoader GoToDream Dream1>>
// <<jump Nothing>>

<<if $hasHeardReaperTerms == false>>
	Steven: H- #e:worried
	<<wait 2>>
	Steven: Hello? #e:embarassed
	<<wait 3>>
	Steven: who are you? #e:yikes
	<<wait 2>>
	Reaper: Oh.. Steven. You're here.
	Reaper: I've been meaning to talk to you.
	<<wait 1>>
	Steven: ... #e:worried
	<<wait 2>>
	Reaper: I'm the Grim Reaper, of course.
	<<wait 2>>
	Reaper: Surely you know that? #e:scary
	<<wait 3>>
	Reaper: Anyways...
	Reaper: I've interrupted your normal dream programming tonight for a reason.
	<<wait 2>>
	Steven: ... #e:think1 
	<<wait 2>>
	Reaper: You're just... 
	<<wait 2>>
	Reaper: How do I put it... #e:shuteye
	<<wait 1>>
	The Reaper is visibly confuzzled...
	<<wait 1>>
	Reaper: You're... A COMPLETE AND UTTER FAILURE! #e:scary
	<<wait 2>>
	Reaper: And I feel bad about reaping your life... #e:smile
	<<wait 2>>
	Steven: ... #e:worried
	<<wait 1>>
	Reaper: There! I said it... #e:shuteye
	<<wait 2>>
	Steven: I'm sorry, I don't get it.. #e:worried
	<<wait 2>>
	Reaper: You see, you were scheduled to die tonight in your sleep.. 
	<<wait 2>>
	Reaper: Normally, I have no issues reaping souls... Especially for people who have lived out a good life. #e:shuteye
	<<wait 2>>
	Reaper: But, your life is just such an utter...
	Reaper: abject... undeniable failure! #e:smile
	<<wait 2>>
	Reaper: And I just... couldn't bring myself to reap such a PATHETIC life! #e:smile
	<<wait 2>>
	Reaper: How do you have no friends, crippling depression, and no happiness left?
	Reaper: Never kissed a girl... good at nothing...
	Reaper: Narcissistic, internet addiction... ZERO personality..
	Reaper: Never spoke up for yourself once, never believed in a cause...
	Reaper: Haven't hugged someone since childhood? And the person you hugged was your mom?
	Reaper: Are you serious? #e:shuteye
	<<wait 1>>
	Reaper: All at the age of 20? 
	<<wait 2>>
	Steven: ... #e:oshit
	<<wait 2>>
	Steven: Am I getting flamed by the grim reaper? #e:oshit
	Voice: This is great! #e:laugh
	<<wait 2>>
	Steven: Voice? You're still here? 
	<<wait 2>>
	Voice: Of course, Steven. 
	Voice: I can follow you into your dreams... #e:evil1
	<<wait 1>>
	Reaper: Look, I get this is weird. But I feel bad for you. 
	Reaper: I believe you can do better. #e:smile
	Reaper: So, I've decided to hold off on your death for now. 
	<<wait 2>>
	Reaper: That is, if you agree to the following terms. #e:smile
	<<jump Hear_Terms>>
	<<wait 3>>
	Steven: Is this guy really able to predict my death..? #e:stern
	Steven: If its the real grim reaper, I should probably be more careful about what I say... #e:think1 
	<<wait 2>>
	Steven: He literally just said I was scheduled to die this year. #e:think2
	<<wait 2>>
	Steven: But it's not like I would care much if that happened... #e:bored
	Steven: I mean, my life is an absolute waste of oxygen... #e:bored
	Steven: Would anyone really care if I dissapeared tomorrow? #e:weary
	<<wait 1>>
<<endif>>
Reaper: Hello, Steven. 
Reaper: So, are you ready for tonight's dream? #e:smile
<<wait 1>>
-> Yes
	Steven: Yes. #e:worried.
-> Not yet
	Steven: Could you give me a moment? #e:shy
	<<wait 2>>
	Reaper: Sure. #e:smile
	Reaper: But hurry up. 
	<<wait 1>>
	Reaper: I've got lots of souls to reap tonight. #e:scary

// teleport to the right dream world
<<if $day == 2>>
	<<playCutsceneAndFade LevelLoader GoToDream Dream1>>
<<elseif $day == 3>>
	<<playCutsceneAndFade LevelLoader GoToDream Dream2>>
<<elseif $day == 3>>
	<<playCutsceneAndFade LevelLoader GoToDream Dream3>>
<<elseif $day == 3>>
	<<playCutsceneAndFade LevelLoader GoToDream Dream4>>
<<elseif $day == 3>>
	<<playCutsceneAndFade LevelLoader GoToDream Dream5>>
<<endif>>
===


title: Hear_Terms
---
Reaper: Here's how it will work... #e:smile
<<wait 1>>
Reaper: I'll play a dream for you each night.
<<wait 2>>
Reaper: These dreams are from your past, present, and future... #e:shuteye 
<<wait 1>>
Reaper: If by the end of the dreams...
Reaper: Your life path changes to something more worthwhile..
Reaper: I will let you keep living. #e:smile
<<wait 2>>
Reaper: But if you're going to end up the same way that you're currently going... #e:shuteye
<<wait 1>>
Reaper: Then... Well...
<<wait 2>>
Reaper: Let's just say, killing you would be a favor. #e:scary
<<wait 1>>
Reaper: So... do we have a deal? #e:smile
<<wait 2>>
-> Yes
	Steven: Yes... #e:worried
	<<wait 2>>
	Reaper: Alright... 
	<<wait 0.5>>
	Reaper: I'm glad we could come to a conclusion. #e:smile
	Reaper: Let's get started with tonight's dream. 
	Reaper: Just get into the boat, and row straight forward. #e:smile
-> No
	Steven: No... #e:stern
	<<wait 1>>
	Voice: Good choice... I don't trust this guy... #e:concerned
	<<wait 2>>
	Reaper: Okay... #e:shuteye
	Reaper: Then I'll kill you RIGHT NOW! #e:scary
	Steven: .. #e:oshit
	Voice: You pissed him off! #e:sad
	Voice: Quickly, apologize! #e:sad
	Steven: I'm sorry... #e:yikes
	<<wait 2>>
	Reaper: Hahaha #e:smile
	Reaper: I'm just kidding... #e:smile
	<<wait 2>>
	Reaper: I care about you, Steven. 
	Reaper: I'm not like the other grim reapers. #e:smile
	<<wait 2>>
	Reaper: Can't you tell I'm trying to help you? #e:smile 
	<<wait 1>>
	Steven: Yes? #e:cringe 
	<<wait 2>>
	Reaper: Anyways, let's get started. I won't take no for an answer. #e:smile
	Reaper: Preparing these dreams took me a while, you know.
<<set $hasHeardReaperTerms = true>>

<<playCutsceneAndFade LevelLoader GoToDream DreamDay1>>

===