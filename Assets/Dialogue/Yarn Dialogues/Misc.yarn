title: Declarations
---
// one time events
<<declare $hasBrainSmashedGlassYet = false>>
<<declare $acceptedBibleStudy = false>> // by default we haven't met stacy yet
<<declare $hasGoneToBibleStudy = false>>
<<declare $saidStacyWasHotInFrontOfLaura = false>>
<<declare $coomerWasExposed = false>>
<<declare $hasHeardReaperTerms = false>>
<<declare $isKYS = true>>
<<declare $firstTimeSleeping = true>>
<<declare $hasDoneFirstFriend = false>>

// reset these every day
<<declare $teethBrushed = false>>
<<declare $isBedroomLampOn = false>>
<<declare $numTimesNutted = 0>>
<<declare $wasNiceToRoomates = false>>
<<declare $talkedToNikolaiAlready = false>>
<<declare $talkedToKabowskiAlready = false>>
<<declare $talkedToBrainAlready = false>>
<<declare $talkedToStacyAlready = false>>
<<declare $talkedToBeckyAlready = false>>
<<declare $talkedToDoomerAlready = false>>
<<declare $talkedToDoomerGirlAlready = false>>
<<declare $talkedToPepeAlready = false>>
<<declare $talkedToBoomerAlready = false>>
<<declare $talkedToBloomerAlready = false>>
<<declare $talkedToCoomerAlready = false>>
<<declare $talkedToRedditAlready = false>>
<<declare $talkedToDiscordAlready = false>>
<<declare $alreadyCookedToday = false>>
<<declare $hasEatenSnack = false>>
<<declare $hasTalkedToReaper = false>> 
<<declare $hasRanToday = false>>
<<declare $hasMicrowavedToday = false>>

// local variables
<<declare $contentednessNerf = 0>>
<<declare $entertainedBuff = 0>>

// variables set by c# code and events (time + location based)
// <<declare $minTilNextBus = 10>>
<<declare $day = 1>>
<<declare $isInNikolaiRoom = false>>
<<declare $gameHour = 0>>

// time events
<<declare $isBibleStudyOpen = false>>
<<declare $isStalkingStacy = false>>
<<declare $isFootballGame = false>>
<<declare $isGamingClub = false>>
<<declare $hasGoneToGamingClub = false>>
// total counts
<<declare $numTimesNiceToRoomates = 0>>
<<declare $kabowskiFriendProgress = 0>>
<<declare $nikolaiFriendProgress = 0>>
<<declare $brainFriendProgress = 0>>
<<declare $stacyFriendProgress = 0>>
<<declare $beckyFriendProgress = 0>>
<<declare $doomerFriendProgress = 0>>
<<declare $doomerGirlFriendProgress = 1>>
<<declare $pepeFriendProgress = 0>>
<<declare $boomerFriendProgress = 1>>
<<declare $bloomerFriendProgress = 0>>
<<declare $coomerFriendProgress = 0>>
<<declare $numTimesCoomerExposed = 0>>
<<declare $RedditFriendProgress = 0>>
<<declare $DiscordFriendProgress = 0>>


===

title: LowEnergy
---
Steven: Jeez, I'm tired... #e:weary
Steven: I should probably sleep soon. #e:weary
===

title: OutOfEnergy
---
Steven: M-must... 
Steven: stay... #e:weary
Steven: awake- #e:bored
<<wait 2>>
<<playCutscene LevelLoader EnergyZero>>
===

title: DoorIsntOpen
---
<<playSoundString AudioManager LockedDoor>>
<<wait 3>>
Steven: The door isn't open. #e:sad
Knock?
-> Yes
    <<KnockOnDoor Manager>>
-> No
===


title: GoToSleep
---
Game: What do?
-> Sleep
	Steven: It's pretty late... #e:think1
	Steven: Should I sleep? #e:stern
	-> Yes
		Steven: Alright... #e:dsmile
		<<goToSleep LevelLoader>>
	-> Hell No!
-> Phone
	<<if $didPhone == true>>
		Steven: I already played with my phone for way too long today... #e:weary
		Steven: I need to do something else.
	<<else>>
		<<playCutscene LevelLoader PlayWithPhone>>
	<<endif>>

===

title: CantGoToSleep
---
<<if $day == 1>>
	Steven: Ah yes, the bed.
	Steven: Where I spend 90% of my waking time.
	Steven: Staring at the ceiling, waiting for the day to end.
	<<wait 1>>
	Steven: And that's 8 hours where I don't need to be awake. 
<<elseif $day == 2>>
	Steven: I can't sleep yet.
	Steven: I know there's nothing to do, but lying in bed will only make me feel worse.
	Steven: Let's try to find something to do, my problems won't fix themselves.
<<endif>>
===

title: EndOfDayDialogue
---
// shit ok so this is 1 above what it should be..
// day 1's end of day is actually $day = 2. So I'll just do $day - 1
// because goToSleep is called before this, which actually increments the day by 1 already

<<if $day-1 == 1>>
    Steven: What an utterly unproductive day. #e:weary
    <<wait 2>>
	Steven: I can't wait for the first week of school! #e:smirk
	<<wait 1>>
	Steven: ...is what I used to say as a kid. #e:think1
	Steven: What a joke.
	<<wait 2>>
	Steven: Should I even go tomorrow? #e:huh
	Steven: I don't want to deal with syllabuses, professor, and icebreakers... #e:cringe 
	Steven: It's all so cringe... #e:bored
	Voice: Yeah, but what if you miss an important assignment on the first day?
	<<wait 1>>
	Steven: You're right man, professors love to do that shit.. #e:bored
	Steven: Okay, I'll go. Better get to sleep, then. #e:stern
<<elseif $day-1 == 2>>
	Steven: What a long day. #e:weary
	Steven: Probably my most eventful day in years... #e:dsmile
	<<wait 1>>
	Steven: Which is funny, because I didn't even do that much. #e:sad
	<<wait 1>>
	Steven: Walking to lecture, talking to roommates, and a random girl... #e:stern
	Steven: Most people would shrug that off as a normal day in the life. #e:think1 
	<<wait 1>>
	Steven: But it's something special for me, because I can't do it often... #e:sad
	<<wait 1>>
	Steven: Oh well, at least I can go to sleep today, knowing that I tried my best. #e:comfy
<<else>>

<<endif>>
===

title: Lamp_On
---
Game: Turn off?
-> Yes
	<<set $isBedroomLampOn to false>>
	<<toggleLight Lamp false>>
-> No
===

title: Lamp_Off
---
Game: Turn on?
-> Yes
	<<set $isBedroomLampOn to true>>
	<<toggleLight Lamp true>>
-> No
===

title: Knock_No_Response
---
Game: There was no response...
===

title: Content_Low
---
<<FadeIn LevelLoader 1.0>>
<<wait 1>>
Steven: I just wanted to stop to say something. #e:stern
<<wait 2>>
Steven: There's something missing. #e:huh
Steven: I don't feel content with my life... #e:sad
<<wait 1>>
Steven: What is it? #e:bored
Steven: Maybe I need to do more wholesome things? #e:think1
Steven: Volunteering, exercising, socializing? #e:huh
<<wait 1>>
Steven: Getting good at a hobby? Earning tons of money? #e:think2 
Steven: How do I make this feeling of meaninglessness go away...? #e:sad
<<FadeOut LevelLoader 1.0>>
<<wait 1>>
===

title: Content_Zero
---
<<FadeIn LevelLoader 1.0>>
<<wait 1>>
Steven: That's it. #e:bored
Steven: There's no fuckin meaning to this dogshit life. #e:bored
Steven: Maybe I should stop trying to find it. #e:think1 
Steven: Life is just about suffering as least as possible until you die, right? #e:stern
Steven: What more is there to it than that? #e:stern
<<wait 2>>
Steven: I'll never be truly content. #e:before-cry
Steven: So why even try? #e:cry-painful
<<wait 4>>
Voice: ... #e:concerned
<<FadeOut LevelLoader 1.0>>
<<wait 1>>
===

title: Social_Low
---
<<FadeIn LevelLoader 1.0>>
<<wait 1>>
Voice: How are you doing, Steven? 
Steven: Oh hey voice...
Steven: I'm good, thanks for asking. #e:smile 
<<wait 3>>
Voice: No, REALLY, how are you doing? 
Voice: Are you happy? #e:think
Steven: Yeah, well, it's complicated... #e:cringe2
<<wait 2>>
Voice: Why's it complicated?
Steven: Oh you know, I have no friends.. #e:dsmile
<<wait 2>>
Voice: Aren't we friends? 
<<wait 2>>
Steven: ...
Steven: I guess.. #e:think1
<<wait 5>>
Voice: Hey Steven, if I was a real person, would you think different of me? #e:think 
<<wait 2>>
Steven: Probably not. #e:bored
<<wait 2>>
Voice: Heh, why's that? #e:laugh
<<wait 2>>
Steven: I'd still want to smack the shit out of you. #e:cry-angry 
<<wait 2>>
Voice: ... #e:laugh 
<<wait 4>>
Voice: I'm here for you, buddy. 
Voice: Tell me all your problems. #e:evil1 
<<FadeOut LevelLoader 1.0>>
<<wait 1>>
===

title: Social_Zero
---
<<FadeIn LevelLoader 1.0>>
<<wait 1>>
Steven: It all just doesn't matter... #e:before-cry
Steven: No one cares about me. #e:cry-shuteye
Steven: No matter how lonely I am... #e:cry-shuteye
Steven: No matter how much I really want to talk to people... #e:cry-shuteye
<<wait 2>>
Steven: My social anxiety prohibits me from making any progress... #e:cry
<<wait 2>>
Steven: I just want someone who understands me... #e:before-cry
Steven: Is that too much to ask for? #e:cry
<<wait 2>>
Steven: Why am I like this? #e:cry-painful
<<wait 4>>
Steven: I just long to be normal, is that so much to ask? #e:cry
Steven: I just want someone to ask me how I'm doing... #e:cry-shuteye
Steven: Someone to send stupid memes to... #e:cry-painful
Steven: Someone to share the moments in my life with... #e:cry-painful
<<wait 4>>
Steven: But I have no one! #e:cry
<<wait 2>>
Steven: ABSOLUTELY FUCKING NO ONE! #e:cry
<<wait 1>>
<<playSoundString AudioManager ManCry>>
<<wait 18>>
Steven: I need to face the news...
Steven: I'm a defective human... trying to live in a world I don't belong in. #e:before-cry
Steven: A world that would be better off without me. #e:cry
<<wait 4>>
Voice: ... #e:concerned
<<FadeOut LevelLoader 1.0>>
<<wait 1>>
===

title: Entertained_Low
---
<<FadeIn LevelLoader 1.0>>
<<wait 1>>
Steven: I'm so fuckin bored... man. #e:weary
Steven: If I don't do something fun soon... I swear to god. #e:bored
Steven: I'm gonna actually kill myself... #e:weary
<<wait 2>>
Steven: There's only so much work I can do before I crash and burnout... #e:sad
<<FadeOut LevelLoader 1.0>>
<<wait 1>>
===

title: Entertained_Zero
---
<<FadeIn LevelLoader 1.0>>
<<wait 1>>
Steven: That's it. I'm done. #e:cry
Steven: Nothing in life is fun. Nothing is exciting. #e:cry-angry
Steven: Everyday is the same old, boring drull. #e:cry-painful 
Steven: I've seen it all. Why not just end myself? #e:cry-angry
<<wait 2>>
Steven: I can't gain pleasure from anything, anyways. #e:cry
<<wait 2>>
Steven: And what's the point of living if I can't have fun anymore? #e:before-cry
<<wait 2>>
Steven: Everything was so colorful and fun when I was a kid. #e:sad
Steven: But now, everything's dull. #e:before-cry
Steven: And I'm supposed to stay on this earth for 50 more years? Suffering from boredom? #e:cry-angry
Steven: Fuck that. #e:angry2
<<wait 4>>
Voice: ... #e:concerned
<<FadeOut LevelLoader 1.0>>
<<wait 1>>
===


title: COOM_DEMON
---
<<if $day == 1>>
	Steven: I'll worry about this tomorrow... #e:think2
	<<jump Nothing>>
<<endif>>
Steven: Would you like to summon... #e:stern
Steven: The coom demon? #e:smirk
<<stopAllMusic MusicObject 1.0>>
<<wait 1>>
<<playMusicName MusicObject StandingHere>> // xD
<<if $numTimesNutted > 0>>
	Steven: Be warned that summoning the coom demon multiple times a day is not recommended. #e:laugh
	Steven: Side effects include lethargy, a dose of shame, and poor performance in bed. #e:huh
	<<wait 2>>
	Steven: As if that concerns me... I already have all those things. #e:bored
<<endif>>

-> Yes
	Steven: If you say so... #e:stern
	<<stopAllMusic MusicObject 1.0>>
	<<wait 1>>
	<<playCutscene LevelLoader Coom>>
-> No
	<<stopAllMusic MusicObject 1.0>>
	<<wait 1>>
	Steven: I'm on a nofap streak.. #e:smirk
===

title: PostNutClarity
---
<<set $contentednessNerf = round(random_range(1, 4) * 10)>>
<<if $numTimesNutted > 0>>
	<<set $contentednessNerf = $contentednessNerf + 5*$numTimesNutted>>
<<endif>>
<<if $contentednessNerf >= 30>>
	Game: The post nut clarity hits you hard...
	Steven: What the hell am I doing with my life? #e:weary
	Game: You lose {$contentednessNerf} contentendess!
	<<changePlayerScoreString Manager "contentedness" {-1 * $contentednessNerf}>>
<<else>>
	Game: The post-nut clarity hits you, but you're used to it now.
	Steven: A necessary sacrifice. #e:stern
	Game: You lose {$contentednessNerf} contentendess!
	<<changePlayerScoreString Manager "contentedness" {-1 * $contentednessNerf}>>
<<endif>>

<<set $entertainedBuff = round(random_range(0, 6) * 5)>>
<<if $entertainedBuff >= 15>>
	Steven: That did feel pretty good, though. #e:comfy
	Game: You gain {$entertainedBuff} entertainment!
	<<changePlayerScoreString Manager "entertained" {$entertainedBuff}>>
<<else>>
	Game: Your dopamine receptors don't get much pleasure from this anymore..
	Steven: Even jerking off doesn't even make me feel better anymore... #e:sad
	Game: You gain {$entertainedBuff} entertainment.
	<<changePlayerScoreString Manager "entertained" {$entertainedBuff}>>
<<endif>>
<<set $numTimesNutted = $numTimesNutted + 1>>
===

title: WalkToCampus
---
Steven: Walk to campus? (-5% energy, +20 minutes, +5% contentedness) #e:worried
-> Yes
	<<jump WalkToSchool>>
-> No
	Steven: Look, I hate the bus too. But this is the best option. #e:sad
===

title: WalkToSchool
---
	Steven: Alright. At least I can enjoy some nature... #e:huh 
    <<changePlayerScoreString Manager "contentedness" 5.0>>
    <<changePlayerScoreString Manager "energy" -5.0>>
    <<AdvanceXMinutes TimeManager 20>> 
	<<FadeIn LevelLoader 1.0>>
	<<playSoundString AudioManager AutumnWalk>>
	<<wait 6>>
    <<yarnLoadScene LevelLoader Campus 5.0 77.1 -38.4>>
	<<wait 2>>
===

title: WalkToHome
---
Steven: Walk home? (-5% energy, +20 minutes, +5% contentedness) #e:huh 
-> Yes
	<<jump Walk_Home_2>>
-> No, I'll ride the bus
	Steven: Ok. #e:stern
===

title: Walk_Home_2
---
	Steven: Alright. #e:smile
    <<changePlayerScoreString Manager "contentedness" 5.0>>
    <<changePlayerScoreString Manager "energy" -5.0>>
    <<AdvanceXMinutes TimeManager 20>> 
	<<FadeIn LevelLoader 1.0>>
	<<playSoundString AudioManager AutumnWalk>>
	<<wait 6>>
    <<yarnLoadScene LevelLoader OutsideHouse 5.0 28.7 -28.8>>
===

title: AlreadyTalkedToday
---
Mind: I already talked to them today... 
Mind: It might be awkward if I talk to them again so soon. #e:stern
Mind: Let's wait until later. #e:shy
===

title: BibleStudy
---

Chase: Everyone! Let's get started. 
<<wait 1>>
Chase: Actually, before we begin, let's go around the room and introduce ourselves.
<<wait 1>>
Chase: Steven, how about you go first?
===

title: BibleStudy_Introductions
---
Introduce yourself?
-> Yes
	Steven: Uhh, h-hi guys, I'm Steven. #e:shy
	<<wait 2>>
	Voice: Tell them more, dumbass...
	<<wait 2>>
	Steven: My favorite color is Red. 
	<<wait 2>>
	Voice: No! Not that! Haha you sound so stupid. #e:cringe
	Steven: Uhhh... I'm a junior...
	<<wait 4>>
	Steven: Uhm...
	<<wait 2>>
	Mind: Shit! I'm freezing up... This is bad... #e:oshit
	<<changePlayerScoreString Manager "social" -40.0>>
	<<wait 2>>
	Chase: Thanks for introducing yourself, Steven! I love the color red too! #e:big-smile
	<<changePlayerScoreString Manager "social" 30.0>>
	<<wait 2>>
	Mind: At least Chase is nice enough to cover up for me. #e:weary
	Voice: You did so bad! #e:laugh
-> Hell No
	<<wait 6>>
	Chase: It's okay if you don't want to. Guys, this is Steven. 
	<<wait 3>>
	Voice: You awkward fuck, you shoulda just introduced yourself. 
	Voice: Look at them, judging you. #e:laugh
	<<changePlayerScoreString Manager "social" -20.0>>
	<<wait 3>>
<<jump Other_Introductions>>
===

title: Other_Introductions
---
Chase: Let's go around the room.
<<wait 2>>
Keith: Hi, I'm Keith. I'm a sophomore.
<<wait 2>>
Pepe: Hello, I'm Pepe. I'm studying mechanical engineering.
<<wait 2>>
Laura: Hey, I'm Laura.
<<wait 2>>

Voice: Awkward... #e:cringe
-> Say something back to everyone
	Steven: Nice to meet you guys...
-> Say nothing
	<<wait 4>>
	<<changePlayerScoreString Manager "social" -10.0>>
	Voice: You probably shoulda said something... #e:cringe
===

title: Stacy_Arrive
---
Stacy: I'm here! 
<<wait 1>>
Chase: Hey Stacy! Perfect timing, we were just gonna start.
Stacy: yay! How's everyone doing?
<<wait 2>>
Pepe: Good, I did the prayer thing you told me to do last time.
<<wait 2>>
Stacy: Awesome! Did you feel God's presence with you at all?
<<wait 1>>
Pepe: Yeah, a bit. I guess it helped me feel a bit better. #e:smile
Pepe: But it still feels like I'm talking to no one at times. #e:sad
<<wait 2>>
Chase: God speaks to each of us in his own way, Pepe.
Chase: I'm glad that you are trying!
<<wait 1>>
Stacy: How about you, Keith? How was your day?
Keith: Pretty rough. I tried my best to avoid my, uh...
Keith: My... problems #e:sad
<<wait 1>>
Chase: Thanks for sharing, Keith. We all deal with temptation in various ways.
Chase: But remember, God has blessed us with the Holy Spirit, which is stronger than all evil.
Stacy: We will pray for you, Keith! #e:smile
Keith: T-thanks, stacy!
<<wait 4>>
Laura: I'm assuming I need to share? #e:looking
Stacy: Only if you want, Laura! #e:smile
Laura: ...
Laura: I guess I tried reading the Bible a bit.
Laura: Not that any of it really makes sense to me... #e:looking
<<wait 1>>
Laura: I still don't really get what Christianity is about.
Laura: Do you guys really believe in a heaven and hell? #e:neutral
Laura: That somehow, magically, we will live past death? It sounds like a fairytale to me.
<<wait 2>>
Stacy: It is not the work of magic, but faith, that keeps us steady, my dear friend.
Stacy: The world would be bleak if not for the sacrafice of Jesus. #e:smile
<<wait 1>>
Chase: Amen. #e:slight-smile
Chase: We'll learn more about this in the coming weeks. I'm glad you started to read ahead! #e:big-smile
<<wait 3>>
Stacy: Steven! I'm going to pick on you! Tell us why you decided to come today. #e:smile
<<wait 1>>
Voice: Shit! Don't mess this up! #e:laugh
-> I want to make new friends
	Steven: I, er...
	<<wait 1>>
	Steven: I want to make new friends... #e:embarassed
	<<wait 2>>
	Pepe: That's why I came originally, too. #e:smile
	Pepe: Everyone here is really nice, you'll like it.
	<<wait 1>>
	Chase: Just like he said, we're your friends now!
	Chase: Feel free to ask us for anything, even outside of here.
	<<wait 1>>
	-> Thanks, guys
	-> ...
-> I think you're really hot
	Steven: Because you look absolutely stunning tonight.
	<<wait 2>>
	<<playSoundString AudioManager crickets>>
	<<wait 5>>
	<<changePlayerScoreString Manager "social" -10.0>>
	Voice: Hahaha what are you doing!?
	Stacy: Uhm... thanks, Steven. #e:laughing
	<<wait 1>>
	<<set $saidStacyWasHotInFrontOfLaura = true>>
-> I want to learn more about God
	Steven: I guess I want to learn more about religion...
	Steven: I've never gotten into the whole "church" thing.
	<<wait 1>>
	Chase: Well that's why we're here! #e:slight-smile
	Chase: It's suprising since in today's world, not many people care or know about God.
	Chase: No one wants to be bossed around, or follow rules.
	<<wait 1>>
	Stacy: But being a Christian is much more than that! It's not just a rulebook.
	Stacy: It's a calling, a purpose in an otherwise bleak life. #e:smile
	<<wait 2>>
	Stacy: My roommate, Laura, is also new here. She's trying to grapple with it as well, so don't worry! 
	<<wait 1>>
	Laura: ... 
	Laura: I only came here because you told me to... #e:looking
	Laura: But I guess it's been a nice change of pace from my routine life #e:looking-away-smile
	Stacy: Exactly! A little force was required to get her to show up, but she enjoys it #e:laughing

<<if $saidStacyWasHotInFrontOfLaura == true>>
	Chase: Well, err, let's get rolling, shall we! #e:confused
<<else>>
	Chase: Well, I'm glad we could all meet one another. Let's get going!
<<endif>>
===

title: After_Sermon
---
	Stacy: Thank you for coming, everyone!
	Chase: Can we just come together for a quick prayer?
	<<FadeIn LevelLoader 1.0>>
	<<playSoundString AudioManager Prayer>>
	<<wait 8.5>>
	<<FadeOut LevelLoader 1.0>>
	Chase: Have a great week, guys!
	<<wait 1>>
	Mind: That was fun. It feels good to be around people, even though I may not be very religious.
	<<wait 2>>
	Voice: You still did a terrible job #e:laugh
	<<wait 1>>
	Mind: Whatever. I didn't expect to do great. At least some people know who I am now.
	<<changePlayerScoreString Manager "social" 50.0>>
	<<set $hasGoneToBibleStudy = true>>
	<<SetTime TimeManager 20 50 0>>
===

title: Talk_To_Reaper
---
// test the cutscene (skip the dialogue)
<<if false>>
	<<playCutsceneAndFade LevelLoader GoToDream DreamDay2>>
<<else>>
	<<if $hasHeardReaperTerms == false>>
		Steven: H- #e:worried
		<<wait 2>>
		Steven: Hello? #e:embarassed
		<<wait 3>>
		Steven: who are you? #e:yikes
		<<wait 2>>
		Reaper: Oh.. Steven. You're here.
		Reaper: I've been meaning to talk to you.
		<<wait 1>>
		Steven: ... #e:worried
		<<wait 2>>
		Reaper: I'm the Grim Reaper, of course.
		<<wait 2>>
		Reaper: Surely you know that? #e:scary
		<<wait 3>>
		Reaper: Anyways...
		Reaper: I've interrupted your normal dream programming tonight for a reason.
		<<wait 2>>
		Steven: ... #e:think1 
		<<wait 2>>
		Reaper: You're just... 
		<<wait 2>>
		Reaper: How do I put it... #e:shuteye
		<<wait 1>>
		The Reaper is visibly confuzzled...
		<<wait 1>>
		Reaper: You're... A COMPLETE AND UTTER FAILURE! #e:scary
		<<wait 2>>
		Reaper: And I feel bad about reaping your life... #e:smile
		<<wait 2>>
		Steven: ... #e:worried
		<<wait 1>>
		Reaper: There! I said it... #e:shuteye
		<<wait 2>>
		Steven: I'm sorry, I don't get it.. #e:worried
		<<wait 2>>
		Reaper: You see, you were scheduled to die tonight in your sleep.. 
		<<wait 2>>
		Reaper: Normally, I have no issues reaping souls... Especially for people who have lived out a good life. #e:shuteye
		<<wait 2>>
		Reaper: But, your life is just such an utter...
		Reaper: abject... undeniable failure! #e:smile
		<<wait 2>>
		Reaper: And I just... couldn't bring myself to reap such a PATHETIC life! #e:smile
		<<wait 2>>
		Reaper: How do you have no friends, crippling depression, and no happiness left?
		Reaper: Never kissed a girl... good at nothing...
		Reaper: Narcissistic, internet addiction... ZERO personality..
		Reaper: Never spoke up for yourself once, never believed in a cause...
		Reaper: Haven't hugged someone since childhood? And the person you hugged was your mom?
		Reaper: Are you serious? #e:shuteye
		<<wait 1>>
		Reaper: All at the age of 20? 
		<<wait 2>>
		Steven: ... #e:oshit
		<<wait 2>>
		Steven: Am I getting flamed by the grim reaper? #e:oshit
		Voice: This is great! #e:laugh
		<<wait 2>>
		Steven: Voice? You're still here? 
		<<wait 2>>
		Voice: Of course, Steven. 
		Voice: I can follow you into your dreams... #e:evil1
		<<wait 1>>
		Reaper: Look, I get this is weird. But I feel bad for you. 
		Reaper: I believe you can do better. #e:smile
		Reaper: So, I've decided to hold off on your death for now. 
		<<wait 2>>
		Reaper: That is, if you agree to the following terms. #e:smile
		<<jump Hear_Terms>>
		<<wait 3>>
		Steven: Is this guy really able to predict my death..? #e:stern
		Steven: If its the real grim reaper, I should probably be more careful about what I say... #e:think1 
		<<wait 2>>
		Steven: He literally just said I was scheduled to die this year. #e:think2
		<<wait 2>>
		Steven: But it's not like I would care much if that happened... #e:bored
		Steven: I mean, my life is an absolute waste of oxygen... #e:bored
		Steven: Would anyone really care if I dissapeared tomorrow? #e:weary
		<<wait 1>>
	<<endif>>
	Reaper: Hello, Steven. #e:smile
	<<jump Ask_If_Ready>>
<<endif>>


===

title: Ask_If_Ready
---
Reaper: Are you ready for tonight's dream? #e:smile
<<wait 1>>
-> Yes
	Steven: Yes. #e:worried.
-> Not yet
	Steven: Could you give me a moment? #e:shy
	<<wait 2>>
	Reaper: Sure. #e:smile
	Reaper: But hurry up. 
	<<wait 1>>
	Reaper: I've got lots of souls to reap tonight. #e:scary

// teleport to the right dream world
<<if $day == 2>>
	<<playCutsceneAndFade LevelLoader GoToDream DreamDay1>>
<<elseif $day == 3>>
	<<playCutsceneAndFade LevelLoader GoToDream DreamDay2>>
<<elseif $day == 4>>
	<<playCutsceneAndFade LevelLoader GoToDream DreamDay3>>
<<elseif $day == 5>>
	<<playCutsceneAndFade LevelLoader GoToDream DreamDay4>>
<<elseif $day == 6>>
	<<playCutsceneAndFade LevelLoader GoToDream DreamDay5>>
<<endif>>
===

title: Boat_Description
---
Steven: It's a boat... #e:stern
<<wait 2>>
Steven: I've always been afraid of boats... #e:cringe
<<wait 2>>
Steven: One time we went kayaking, and our boat almost tilted over.. #e:worried
===
title: Hear_Terms
---
Reaper: Here's how it will work... #e:smile
<<wait 1>>
Reaper: I'll play a dream for you each night.
<<wait 2>>
Reaper: These dreams are from your past, present, and future... #e:shuteye 
<<wait 1>>
Reaper: If by the end of the dreams...
Reaper: Your life path changes to something more worthwhile..
Reaper: I will let you keep living. #e:smile
<<wait 2>>
Reaper: But if you're going to end up the same way that you're currently going... #e:shuteye
<<wait 1>>
Reaper: Then... Well...
<<wait 2>>
Reaper: Let's just say, killing you would be a favor. #e:scary
<<wait 1>>
Reaper: So... do we have a deal? #e:smile
<<wait 2>>
-> Yes
	Steven: Yes... #e:worried
	<<wait 2>>
	Reaper: Alright... 
	<<wait 0.5>>
	Reaper: I'm glad we could come to a conclusion. #e:smile
	Reaper: Let's get started with tonight's dream. 
	Reaper: Just get into the boat, and row straight forward. #e:smile
-> No
	Steven: No... #e:stern
	<<wait 1>>
	Voice: Good choice... I don't trust this guy... #e:concerned
	<<wait 2>>
	Reaper: Okay... #e:shuteye
	Reaper: Then I'll kill you RIGHT NOW! #e:scary
	Steven: .. #e:oshit
	Voice: You pissed him off! #e:sad
	Voice: Quickly, apologize! #e:sad
	Steven: I'm sorry... #e:yikes
	<<wait 2>>
	Reaper: Hahaha #e:smile
	Reaper: I'm just kidding... #e:smile
	<<wait 2>>
	Reaper: I care about you, Steven. 
	Reaper: I'm not like the other grim reapers. #e:smile
	<<wait 2>>
	Reaper: Can't you tell I'm trying to help you? #e:smile 
	<<wait 1>>
	Steven: Yes? #e:cringe 
	<<wait 2>>
	Reaper: Anyways, let's get started. I won't take no for an answer. #e:smile
	Reaper: Preparing these dreams took me a while, you know.
<<set $hasHeardReaperTerms = true>>

<<playCutsceneAndFade LevelLoader GoToDream DreamDay1>>

===

title: Dream_Arrive
---
// <<StartAllMusic MusicObject 1.0>>
// <<wait 2>>
<<set $hasBoated = true>>
<<if $day == 2>>
    <<jump Dream_1_Arrive>>
<<elseif $day == 3>>
	<<jump Dream_2_Arrive>>
<<elseif $day == 4>>
	<<jump Dream_3_Arrive>>
<<elseif $day == 5>>
	<<jump Dream_4_Arrive>>
<<elseif $day == 6>>
	<<jump Dream_5_Arrive>>
<<endif>>
===



title: Dream_2_Arrive
---
Steven: The reaper better not have put anything weird in this dream... #e:huh
Steven: I will fight him if he does... #e:angry3
===

title: Dream_3_Arrive
---
Steven: I don't want this anymore... #e:weary
===

title: Dream_4_Arrive
---
Steven: God, when will it end? #e:cry-painful
Steven: I don't want to see my future! #e:cry
===

title: Dream_5_Arrive
---
Steven: ... #e:oshit
===

title: Got_Text
---
Steven: Hmmm, it's probably my mom again... #e:bored
===

title: After_Look_Phone
---
Steven: Wait, it's not my mom.. #e:huh
<<if $hasMetDoomer == true>>
	Steven: It's Dylan... #e:think1
<<elseif $hasMetPepe == true>>
	Steven: It's Pepe... #e:think1
<<elseif $hasMetDoomerGirl == true>>
	Steven: It's Laura... #e:think1
<<endif>>
<<wait 2>>
Steven: I can't remember the last time a non-family member contacted me...
Steven: I forgot what that notification sound even sounded like... #e:cry
<<wait 3>>
Steven: This is progress... right? #e:cry-smile
Voice: Read the message, numbskull! #e:think
Steven: It says, "what's up"... #e:cry
<<wait 3>>
Steven: What's up... #e:cry-smile
Steven: Such a meaningless phrase, yet they don't know how much it means to me... #e:cry-smile
<<wait 1>>
Steven: Feels good to finally have someone who cares about my existence.. #e:cry
<<wait 2>>
Steven: I'll respond to them later, <color=green>using the computer</color>. #e:cry-smile
Steven: I need to process this first... #e:cry
<<stopAllMusic MusicObject 0.5>>
<<FadeIn LevelLoader 1.0>>
<<wait 1.5>>
<<playSoundString AudioManager ManCry>>
<<wait 18>>
<<FadeOut LevelLoader 1.0>>
<<wait 1>>
<<StartAllMusic MusicObject 0.5>>
Voice: God, that's embarassing. #e:cringe
Voice: You're crying, just for making a friend? #e:concerned
Steven: Shouldn't you understand the best? You've been in my head for decades! #e:cry
Steven: You know how lonely I am. This means everything to me. #e:cry-smile
<<wait 1>>
Voice: Whatever you say...
<<set $hasDoneFirstFriend = true>>
===

title: KYS
---
<<stopAllMusic MusicObject 0.5>>
<<wait 1>>
Voice: What's the matter, Steven?
<<wait 2>>
Steven: Voice... I can't...
Steven: I can't take it anymore.
Voice: Take what anymore? #e:think
<<wait 2>>
Steven: Don't play dumb with me! #e:cry-angry
Steven: You're the only person who really knows what I'm struggling with. #e:cry-angry
Steven: Stop pretending like you don't know! #e:cry-painful
<<wait 5>>
Voice: Well, Steven... #e:think1 
Voice: Maybe the answer was with you all along. #e:evil1
<<wait 2>>
Steven: What do you mean? #e:cry
<<wait 3>>
Voice: You said it yourself... #e:sad
Voice: ...Maybe you're just a lost cause. #e:evil1
Voice: Someone not fit to walk this earth. #e:evil1
<<wait 2>>
Steven: ... #e:cry
<<wait 3>>
Voice: I'll offer you one last piece of advice. #e:concerned 
<<wait 5>>
Voice: <color=orange>Kill yourself.</color> #e:think1
<<wait 5>>
Voice: It's the only answer to escape your shitty life, and you know it. #e:concerned
<<wait 4>>
Steven: ... #e:cry-shuteye
<<wait 4>>
Voice: Think about it this way... #e:concerned
Voice: You've given it your best try. And nothing's improving. #e:concerned
Voice: So what else is there to do? 
Voice: This isn't an emotional or rash decision... it's just simple logic. #e:think
Voice: If things aren't getting better, try something new. #e:think
<<wait 2>>
Steven: But... 
Steven: ...I'm scared of death.. #e:cry-shuteye
<<wait 3>>
Voice: Stay with me, here... #e:think 
Voice: You're a smart guy. #e:concerned
<<wait 2>>
Voice: You know that everyone dies eventually. #e:concerned 
Voice: It's not like you would end up somewhere else by dying now. #e:think
<<wait 2>>
Steven: But.. I'm afraid of pain. #e:cry-shuteye
<<wait 2>>
Voice: You're a smart guy. #e:concerned
Voice: I'm sure you can come up with many ways to do it quickly and painlessly. #e:evil1 
<<wait 5>>
Voice: Look Steven, I've always loved and respected you. Ever since we met. #e:think
Voice: But be honest, haven't you thought that maybe... it's time to let go? #e:concerned
<<wait 2>>
Steven: ... #e:cry-shuteye
<<wait 10>>
<<stopAllMusic MusicObject 0.5>>
<<wait 1>>
Steven: You know what, Voice... thanks. #e:cry
Steven: You're completely right. #e:cry-shuteye
<<wait 2>>
Steven: This is the best piece of advice you've ever given me. #e:cry-smile
Steven: I'm glad I have you with me. #e:cry-smile
Voice: That's what I'm here for. #e:think
<<set $isKYS = true>>
===

title: At_Bus_Stop
---
Steven: Voice...
<<wait 3>>
Steven: Why was I even born? #e:sad
<<wait 4>>
Voice: I don't know the answer to that, Steven. #e:concerned 
<<wait 5>>
Steven: Are you coming with me? #e:sad
<<wait 5>>
Voice: No. #e:concerned 
<<wait 3>>
Voice: I am merely a figment of your imagination. I am you. #e:think
Voice: The moment you die, I cease to exist. #e:think 
<<wait 3>>
Steven: Oh. #e:cry-shuteye
<<wait 2>>
Steven: Was I a good owner? #e:cry
<<wait 2>>
Voice: ... #e:concerned
Voice: I wouldn't trade you for anyone else. #e:think
<<wait 4>>
Steven: Thanks.. #e:cry-smile
<<wait 3>>
Steven: You were the closest thing I had to a friend, you know. #e:cry
<<wait 4>>
Voice: I know. #e:concerned
===

title: Can_Sleep_Now
---
Game: You can now sleep.
<<if $firstTimeSleeping == true>>
	Game: To sleep, go to your bedroom and press E near the bed.
	Game: If you stay up too much, you might find yourself with a lower energy level for the next day.
	<<set $firstTimeSleeping = false>>
<<endif>>
===

title: KYS_Bus
---
Steven: My ride's here. #e:cry-smile
<<wait 1>>
Voice: Goodbye. #e:concerned
===

title: Game_End_KYS
---
<<QuitGame UI>>

===