title: D2_Lancelot_Commons
---
Nikolai: Heylo, Steven, my friend.
Nikolai: How vas your summer?
-> Pretty good, thanks for asking.
    Nikolai: Gud to hear, my friend.
    -> How was your summer?
        Nikolai: Vury gud. I go back to Siberia, my home.
        Nikolai: I drink lot of vodka and play csgo
        <<wait 1>>
        Nikolai: I am glad to be back, Steven
        <<wait 1>>
        Nikolai: Although I will miss my pet bear, Gasha.
    -> Talk to you later.
        Nikolai: Goodbye.
-> Terrible.
    Nikolai: Ah, Blyat. 
    Nikolai: прости за это. School year vill be better.
    Nikolai: Hopevully, ve can see each other more offen.
    Nikolai: Last year I did not get chance to talk to you much.
    <<wait 1>>
    Nikolai: Have you talk to others yet? I zink that zey are in zeir rooms.
===

title: D2_Sleep
---
<<playSoundString AudioManager NotBirthdaySong>>
<<wait 17>>
<<wait 2>>
<<playSoundString AudioManager CandlesBlow>>
<<wait 3>>
A candle with the number "10" on it, is blown out.
<<playMusicName MusicObject RestaurantAmbience>>
<<wait 4>>
The cake is okay. Your parents always order the same cake, so you know exactly what it will taste like.
Out of the corner of your eye, you see something.
Your childhood friend, Ralph, is now waving goodbye.
<<wait 1>>
Ralph: Happy birthday, Steven!
<<wait 1>>
He follows his mother out of the buffet.
<<wait 1>>
A few more minutes pass.
Vincent: Thanks for inviting us over!
Josh: This buffet was the best!
<<wait 1>>
The twins and their mother wave as they leave.
<<wait 1>>
It's just you and your parents now.
<<wait 1>>
Mom: Alright Steven, let's get ready to go.
Dad: I agree, you got school tomorrow.
<<wait 2>>
You exit the buffet with them, into the dark, starry parking lot.
<<stopAllMusic MusicObject>>
<<playSoundString AudioManager OpenShopDoor>>
<<wait 3>>
<<playMusicName MusicObject ParkingLotAmbience>>
<<wait 2>>
You start to cry.
<<wait 2>>
Mom: Why you crying?
<<wait 2>>
You try to stop, but the tears keep coming.
<<wait 2>>
Dad: Was food not good enough?
<<wait 1>>
...
<<wait 2>>
Mom: Don't be ungrateful.
<<wait 2>>
You're sitting on the curb.
<<wait 2>>
Dad: Steven! Get up this instant. Don't throw a tantrum on your birthday!
Your mom holds up a hand as if to signal your dad, "not now".
She puts a hand on your shoulder.
<<wait 1>>
Mom: Tell me what's wrong, honey?
<<wait 3>>
I don't want to get older.
<<wait 2>>
Your mom and dad look at each other, surprised.
Dad: Why is that?
<<wait 1>>
I don't want to die.
<<wait 1>>
Your parents start to laugh. It doesn't make you feel any better.
<<wait 1>>
Mom: That's something you shouldn't worry about right now.
Why? Everybody dies, right?
What's the point of living, if we all die anyway?
<<wait 2>>
Your parents pause to think a little.
Something tells you they don't quite know the answer either.
But back then, you thought they would know.
Dad: You are young. You have a great life to look forward to.
Mom: You should be excited, not sad. 
Dad: Life is like great rollercoaster. You haven't even rode it. And you are already sad?
<<wait 2>>
<<stopAllMusic MusicObject>>
Life is a rollercoaster, huh?
What a joke. There was no rollercoaster.
I have no friends, no achievements, no memories to look back on fondly.
And whose fault is it?
<<wait 2>>
No one but mine.
<<wait 2>>
Just be social. Just be productive. Just live a better life!
<<wait 1>>
Yeah. All of life is just that easy, isn't it?
<<wait 1>>
I'm turning 20 tomorrow. A whole decade has passed since this memory.
And yet it still remains ingrained in my mind.
Because I still feel the same way as I do back then. 
The only thing that has changed is that I now have a large pile of regrets.
Regrets that I have to carry with me, until I die.
<<wait 1>>
Maybe the young version of me sensed something.
It knew I was never going to amount to anything. 
That I wasn't fit to live a good life.
And that was enough to make me cry.
// load user back into bedroom for day 2
<<yarnLoadScene LevelLoader wakeUp 2.0f>>
===

title: D2_StartDay
---
Shoot!
I already overslept...
And what was the heck was that dream?
<<wait 1>>
Oh well, I gotta get to school.
Even I don't ditch the first days. 
It just feels wrong to ditch them, even though nothing important is ever said. 
===

title: D2_Commons_OpenDoor
---
The outside world... how scary.
<<wait 1>>
Everytime I hear the noises of the outside world, a part of me dies.
It reminds me of a society that I'm not a part of in any way.
Here goes nothing...
<<playCutsceneAndFade LevelLoader GoOutside OutsideHouse>>
===

title: D2_Kabowski_Still_Asleep_Knock
---
<<playSoundString AudioManager SnoringSounds>>
<<wait 3>>
Someone isn't attending class today...
===

title: D2_Brain_Annoyed_Knock
---
<<if $hasBrainSmashedGlassYet == false>>
    <<playSoundString AudioManager SmashingGlass>>
    <<wait 5>>
    You hear an interesting array of expletives. 
    Something tells you that you shouldn't bother Brain right now.
    <<set $hasBrainSmashedGlassYet = true>>
<<elseif $hasBrainSmashedGlassYet == true>>
    Brain: Can you leave me alone!?
    Brain: Don't you have your own trivial, worthless matters to attend to?
<<endif>>
===

title: D2_Lancelot_Room_Before_School
---
Nikolai: сука блять! vat are you trying to do?
Nikolai: You go in my room for vat?
===

title: D2_Tried_To_Enter_House_Without_School
---
I still gotta get to school...
Let's go catch the bus.
===

title: Day2_LookAtBusStop
---
This is the bus stop. I need to take it to get to campus.
Otherwise, it's a 20 minute one-way walk.
It'll also cost me some energy.
But it might be nice getting some fresh air... The bus is always crowded and I hate it.
===

title: D2_WalkToCampus
---
Should I walk to campus? (-5% energy, +20 minutes, +5% contentedness)
-> Yes
    <<changePlayerScoreString Manager "contentedness" 5.0>>
    <<changePlayerScoreString Manager "energy" -5.0>>
    <<AdvanceXMinutes Manager 20>> 
    <<yarnLoadScene LevelLoader Campus 2.0>>
-> No
===

title: Day2_LookAtUnion
---
This is the Union, where my classes are held. 
It's quite an old building. 
I should head here next.
===

title: Day2_LookAtGreenStreet
---
This is Green Street, where most of the tall apartments complexes and restaurants are.
It's also where all the partying happens. I wonder what it'd be like to go to a party...
Anyways, I should get to class.
===

title: Day2_LookAtCoffee
---
I've never really liked the taste of coffee, it always makes me feel way too jittery to get anything done.
But I will admit that coffee shops are very nice to sit around in.
For more serious work though, I prefer to be in my room.
===

title: Day2_LectureHall
---
The lecture hall, aka, the room that I visit once and never return to.
Unless, there's an exam or something. Then I have to come here.
Because all my classes record lectures, it's easy to just get lazy and watch them online.
People always say I'm "wasting my money" by not going to lectures, but it's not like I'm learning anything either way.
===

title: Day2_Enter_Lecture_Hall
---
God, I hate opening doors.
The judgemental eyes of people peering at me. 
There's no worse feeling for an introvert.
Oh well, let's make it quick.
I don't care what anyone here thinks of me.
...
Right?
<<playCutsceneAndFade LevelLoader OpenLectureHallDoors LectureHall>>
===

title: Day2_Lecture
---
Huh, it's one of those lecture halls where the doors are in the back.
Thank goodness. No one sees me.
<<wait 2>>
<<playSoundString AudioManager SchoolBell>>
<<wait 3>>
Seems like the lecture is starting, I should sit down.
<<wait 1>>
Shit, the only seat is in the front.
<<wait 1>>
I hate front seats, but I can't just stand. That'd look too awkward.
===

title: Day2_LectureFallAsleep
---
So..
boring... 
<<FadeIn LevelLoader 1.0>>
<<changePlayerScoreString Manager "entertained" -5.0>>
<<playSoundString AudioManager SnoringSounds>>
<<playSoundString AudioManager UncannyLecture>>
<<wait 2>>
<<FadeOut LevelLoader 0.3>>
<<wait 0.5>>
Wake him up?
-> Yes 
    <<jump Day2_LectureWakeup>>
-> Nah (+2% energy)
    <<changePlayerScoreString Manager "energy" 2.0>>
    <<wait 2>>
    <<FadeIn LevelLoader 0.3>>
    <<wait 1>>
<<FadeOut LevelLoader 0.3>>
<<wait 0.5>>
How about now?
-> Ok, wakeup
    <<jump Day2_LectureWakeup>>
-> Nah, keep sleeping (+2% energy)
    <<changePlayerScoreString Manager "energy" 2.0>>
    <<wait 2>>
    <<FadeIn LevelLoader 0.3>>
    <<wait 1>>
<<FadeOut LevelLoader 0.3>>
<<wait 0.5>>
Surely now?
-> Ok, wakeup
    <<jump Day2_LectureWakeup>>
-> No, let him sleep (+2% energy)
    <<changePlayerScoreString Manager "energy" 2.0>>
    <<wait 2>>
    <<FadeIn LevelLoader 0.3>>
    <<wait 1>>
<<FadeOut LevelLoader 0.3>>
<<wait 0.5>>
Ok, he's still sleeping. Surely you don't need more energy?
-> Ok, enough sleeping.
    <<jump Day2_LectureWakeup>>
-> No, the man is tired. (+2% energy)
    <<changePlayerScoreString Manager "energy" 2.0>>
    <<wait 2>>
    <<FadeIn LevelLoader 0.3>>
    <<wait 1>>
<<FadeOut LevelLoader 0.3>>
<<wait 0.5>>
You need to play the game, you know. This lecture doesn't go on forever.
-> Ok, fine.
    <<jump Day2_LectureWakeup>>
-> No, I just wanna sleep (+2% energy)
    <<changePlayerScoreString Manager "energy" 2.0>>
    <<wait 2>>
    <<FadeIn LevelLoader 0.3>>
    <<wait 1>>
<<FadeOut LevelLoader 0.3>>
<<wait 0.5>>
So you played this game to just listen to a history lecture?
-> No, I wanna play now.
    <<jump Day2_LectureWakeup>>
-> Yeah (+2% energy)
    <<changePlayerScoreString Manager "energy" 2.0>>
    <<wait 2>>
    <<FadeIn LevelLoader 0.3>>
    <<wait 1>>
    <<stopSoundStringFade AudioManager UncannyLecture>>
    <<wait 4>>
    <<playCutscene LevelLoader Airhorned>>
===

title: Day2_LectureWakeup
---
<<SetGameVariable Manager "hasFinishedLecture" true>>
<<FadeOut LevelLoader 1.0>>
<<wait 2>>
Woah, did I just fall asleep?
<<wait 2>>
God, I don't remember a thing he said.
<<wait 5>>
Damn, I'm really out of it today.
<<stopSoundStringFade AudioManager UncannyLecture>>
Oh well, looks like lecture is over.
<<turnOnLectureHallStudents CrossSceneObjects>>
<<stopRunningCutscene Manager>> // nvm it works now. This just stops any currently running cutscene, pretty useful
<<changePlayerScoreString Manager "energy" -30.0>>
<<changePlayerScoreString Manager "entertained" -10.0>>
<<changePlayerScoreString Manager "social" -10.0>>
<<changePlayerScoreString Manager "contentedness" 20.0>>
===

title: Day2_DidntWakeup
---
<<SetGameVariable Manager "hasFinishedLecture" true>>
<<changePlayerScoreString Manager "energy" -50.0>>
<<changePlayerScoreString Manager "entertained" -30.0>>
<<changePlayerScoreString Manager "social" -50.0>>
<<changePlayerScoreString Manager "contentedness" -40.0>>
Shit. They're laughing at me.
Game: -50% Social, -30% Contentedness, -30% entertained
Fuck Shit dammit
Why do I embarass myself wherever I go?
Why do I have to be a walking joke? 
<<wait 1>>
I gotta get out of here before I lose more face.
===

title: Day2_Finished_Lecture
---
Well, that was a blast. 
<<wait 1>>
Another day of learning things that I will never once use in my life.
<<wait 1>>
Oh well, At least I tried going to class for once.
+10% Contentedness 
<<changePlayerScoreString Manager "contentedness" 10.0>>
<<wait 2>>
Anyways, I better go home. There's nothing else to do today.
===

title: Day2_Stacy_Hey
---
Hey!
===

title: Day2_Stacy_UpHere
---
Up here!
===

title: Day2_StacyTalk
---
You: S-sorry, I didn't see you there!
<<wait 1>>
Stacy: It's okay!
Stacy: I should be apologizing for bothering you. 
Stacy: I'm Stacy, nice to meet you.
<<wait 4>>
You: Is she waiting for me to introduce myself?
<<wait 1>>
You: My name is-
Stacy: We're-
<<wait 2>>
Shit!
Stacy: Sorry, you go ahead. What's your name?
You: It's S-steven.
<<wait 1>>
Stacy: Nice to meet you Steven!
Stacy: We're having a Bible Study today. Would you like to come?
-> S-sure
    <<wait 1>>
    You: Yeah, sure.
    Stacy: Awesome! 
    Stacy: It's everyday from 5-9pm right here! There will be food, too.
    <<set $acceptedBibleStudy = true>>
-> Not sure
    <<wait 1>>
    You: Sorry, I'm not sure yet...
    Stacy: No problem! It's from everyday from 5-9pm right here, if you wanna come!
    Stacy: We'd be glad to meet you!
-> No
    <<wait 1>>
    You: Um, sorry- I don't really believe in God.
    Stacy: oh- that's okay! If you ever feel like learning more, feel free to come.
    Stacy: We're here everyday at 5pm!
You: o-ok, thanks.
===

title: Day2_After_Stacy
---
<<if $acceptedBibleStudy == true>>
    I'm not sure whether going to this is a great idea.
    But she seemed too nice to turn down.
    <<wait 1>>
    Do I actually go to it?
    -> Yes
        Are you sure? I'll probably get ousted for not being a Christian.
        <<wait 1>>
        I haven't gone to Sunday school in ages.
        And I'm not sure if they're my kind of people...
        <<wait 1>>
        But Christians are usually very nice people.
        And I could use some friends.
        It was nice talking for that brief moment. Rarely happens to me ever.
        <<changePlayerScoreString Manager "loneliness" 10.0>>
        <<wait 1>>
        Anyways, she said 5pm. Guess I'll decide then.
    -> No
        <<jump Day2_Reject_Church>>
<<elseif $acceptedBibleStudy == false>>
    <<jump Day2_Reject_Church>>
<<endif>>
===

title: Day2_Reject_Church
---
Yeah, I think it's just too big of a challenge for me.    
Worship songs, prayer, small groups...
It's just too much for a shut-in.
<<wait 1>>
I don't mind religion, I'm just scared of the social contact.
Also, God forbid I get labelled as one of those dudes who just goes to church to pickup girls.
<<wait 2>>
That girl was super nice, though.
I can't remember the last time a girl talked to me first.
Much less laughed at my joke. 
<<changePlayerScoreString Manager "loneliness" 50.0>>
<<wait 3>>
Wait, she was probably laughing at me. Shit.
<<changePlayerScoreString Manager "loneliness" -40.0>>
<<wait 1>>
God, I must've sounded so weird.
Blargh. If I end up ditching, let's hope I never run into her again.
I think she said 5pm?
<<wait 1>>
Oh well, guess I have some free time for now.
===


title: Day2_Commons_Scene
---
Kabowski: As I was saying Nikolai, White Claws.
Kabowski: They're like the equivalent of vodka here in the USA.
Kabowski: You have got to try them.
<<wait 1>>
Nikolai: vite.. claw.
Nikolai: Iz zis vite claw strong like vodka?
Kabowski: Strong? Yeah dude, if you shotgun a whole pack. 
Kabowski: Have I shown you how to shotgun yet? It's simple. 
Kabowski: Key. Left hand. Or right hand, if you're right handed. 
Kabowski: White claw. Right hand. Punch that sucker right in. Now-
Nikolai: Oh look, Steven iz back.

===


title: Day2_Commons_Scene2
---
Kabowski: Look who it is. Man of the hour. Stevie Wonder.
Kabowski: I missed ya, buddy.
-> Hey Kabowski, how was your summer?
    Kabowski: Lean, mean and pristine. I went to Europe.
    -> Is that so? Where?
    -> What'd you do?
    Kabowski: We went all over- Deutschland, France, Spain.
    Kabowski: Couple of buddies and I. Real low key.
    Kabowski: Who am I kidding, it was a blast.
    Kabowski: Let's see, I got drunk, went to more music festivals I can remember, oh- and I'm still undefeated at beer pong.
    Kabowski: Fucked a couple of real babes, too.
    <<wait 2>>
    Sounds like Kabowski had a fun summer.
    See, this is why I can't stand him.
    Part of me wishes I could be him, and yet part of me feels sorry for him.
    <<wait 1>>
    He lives such a naive life, happy and carefree.
    He wouldn't know a thing about being lonely. About never setting hands on a girl. 
    He wouldn't know a thing about having no friends to ask to travel with. 
    He doesn't understand me.
    <<jump Day2_Commons_Eating>>
-> What's up guys
    Kabowski: Woah, you look much stronger than before. Been going to the gym this summer?
    -> Yes
    -> No
    Kabowski: Well, if you ever need a spotter, you know where to find me.
    Nikolai: Vat is spotter?
    Kabowski: Oh, it's like the dude that makes sure you don't drop the weight on yourself.
    Kabowski: You know bench press Nikolai? Do they have those in Russia?
    <<wait 2>>
    Ah shit. I started Kabowski on some stupid shit.
    Now he's gonna lecture Nikolai on the gym. And Nikolai's too nice to stop him.
    <<wait 2>>
    This is why I hate Kabowski. Everything's about the gym.
    But in many ways, I understand why. 
    His appearance is all he's got going for him.
    And once he gets old, well, what's he going to do?
    <<wait 2>>
    He lives for the here and now. Living the best youth possible is all he cares about.
    And in many ways, he's smart for that. Youth is the peak of life, after all. One that I've utterly wasted.
    <<wait 2>>
    Kabowski's that firework at the firework show that shoots up high into the sky at twice the speed of the others, then explodes in a giant BOOM.
    <<wait 2>>
    Which of course, is better than my life trajectory.
    I would be the firework that gets lit and doesn't go anywhere, then gets tossed out.
    <<jump Day2_Commons_Eating>>
-> Fuck off
    Kabowski: Woah, what'd I do wrong buddy?
    Kabowski: Nikolai, I think Mr. grumpy over there is in a bad mood. 
    Nikolai: Yes, vury bad mud. Maybe you shouldn't talk to him anymur
    <<set $wasNiceToRoomates = false>>
    Screw these losers.
    I've got work to do.
    Well, I don't really. But I just don't want to talk to them. It's too much effort.
    And it's not like I have anything to say to them... or anything in common with them.
    Kabowski is such a dickhead, he'll probably make fun of me or something.
    <<wait 1>>
    And Nikolai... well, is just Nikolai. He doesn't understand half of what I say.
    <<wait 1>>
    God knows what Brain is up to...
    <<wait 1>>
    I hate my fucking roommates. 
    I can't believe I thought I would make close friends with them when I first arrived to college.
    What a joke.
===
title: Day2_Commons_Eating
---
<<set $wasNiceToRoomates = true>>
<<changePlayerScoreString Manager "loneliness" 15.0>>
<<wait 2>>
Anyways, I should leave this conversation. He'll babble on forever.
<<wait 2>>
Nikolai: Hey vait- Steven. Vant to eat vis us tonight? 
<<wait 1>>
You want me to eat with you?
<<wait 2>>
Kabowski: Yeah dude. Nikolai's cooking up some serious macros.
-> Sure
    Nikolai: Okay, great. 
    <<playCutscene LevelLoader EatingDay2>>
-> No thanks
    Nikolai: Aw, ok. See yu later, zen.
===

title: Day2_Nikolai_Food
---
Nikolai: Okay guyz, today I hav here Пирожки, or Pirozhki.
Nikolai: It is like meat pie. I hope yu enjoy.
<<wait 1>>
Kabowski: Hell yeah! Smells amazing, Nikolai.
<<wait 1>>
Nikolai: Thanks. Alzough I am confused. Where is Brian?
<<wait 1>>
Kabowski: Oh, you mean THE almighty Brain? Not sure. Here, I'll call him.
<<wait 2>>
Kabowski: (in a angelic voice) [wave][bounce]oh braaaaaaaaaaainnnnnnn~![/]
<<wait 2>>
Kabowski: [wave][bounce]braaaaaaaaaaainnnnnnnyyyyy boooooo~![/]
<<wait 1>>
Nikolai: Hm, seems he's vury busy.
===

title: Day2_Brain_Exits_Lair
---
Brain: Will You idiots..
<<wait 1>>
Brain: Keep it DOWN?!?
===

